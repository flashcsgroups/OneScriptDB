// MIT License
// Copyright (c) 2018 Vladimir Vasiliev
// https://github.com/vasvl123/OneScriptDB


Перем procid Экспорт;
Перем Шаблон;
Перем Буфер, БуферДанные, БуферУзел;
Перем Хост, Порт;
Перем Вкладки, ВкладкиСписок, ИдВкладки;
Перем ОбновитьВкладки;
Перем ТекущаяВкладка, ТекущиеДанные, ТекущееОкно;
Перем ЗадачиКоличество;
Перем ОбщийРезультат;
Перем КоличествоПопыток;
Перем НомерОбновления;
Перем Команда;
Перем Настройки Экспорт;
Перем ВсеДанные Экспорт;
Перем ИмяДанных;
Перем Журнал;
Перем ЗавершитьПроцесс;
Перем Субъект;
Перем Гость;
Перем ПараметрХост;

Функция РазмерПоля(Знач Стр)
	Размер = Цел(СтрДлина(стр)/3);
	Если Размер < 3 Тогда
		Размер = 3;
	ИначеЕсли Размер > 12 Тогда
		Размер = 12;
	КонецЕсли;
	Возврат Строка(Размер);
КонецФункции


Функция СтрЭкранироватьРазметку(Знач Стр, огрДлины = 0)
	Стр = СтрЗаменить(Стр, "&", "&amp;");
	Стр = СтрЗаменить(Стр, """", "&quot;");
	Стр = СтрЗаменить(Стр, "'", "&#39;");
	Стр = СтрЗаменить(Стр, "<", "&lt;");
	Стр = СтрЗаменить(Стр, ">", "&gt;");
	Стр = СтрЗаменить(Стр, Символы.ПС, "<br/>");
	Если огрДлины > 0 Тогда
		длСтр = СтрДлина(Стр);
		Если длСтр > огрДлины Тогда
			Стр = Лев(Стр, огрДлины) + " ...";
		КонецЕсли;
	КонецЕсли;
	Возврат Стр;
КонецФункции


// Функция СтрокаЗапрос(ПараметрыЗапроса)
// 	Стр = "";
// 	Для каждого Параметр Из ПараметрыЗапроса Цикл
// 		Если Стр = "" Тогда
// 			Стр = "?";
// 		Иначе
// 			Стр = Стр + "&amp;";
// 		КонецЕсли;
// 		Стр = Стр + Параметр.Ключ + "=" + Параметр.Значение;
// 	КонецЦикла;
// 	Возврат "/showdata/" + procid + Стр;
// КонецФункции


Функция ПоказатьВкладки()
	Текст = "";
	ПараметрыШаблона = Новый Структура;
	Для каждого элВкладка Из ВкладкиСписок Цикл
		Вкладка = элВкладка.Значение;
		ПараметрыШаблона.Вставить("ПараметрВкладка", Вкладка.ИдВкладки);
		ПараметрыШаблона.Вставить("ПараметрЗаголовок", Вкладка.Заголовок);
		ПараметрыШаблона.Вставить("ПараметрАктивный", ?(Вкладка.ИдВкладки = ТекущаяВкладка, "active", ""));
		ПараметрыШаблона.Вставить("ПараметрРежим", Вкладка.Режим);
		ПараметрыШаблона.Вставить("ПараметрТипВкладки", Вкладка.ТипВкладки);
		ПараметрыШаблона.Вставить("ПараметрИдВкладки", Вкладка.ИдВкладки);
		ПараметрыШаблона.Вставить("ПараметрПрокрутка", Вкладка.Прокрутка);
		ПараметрыШаблона.Вставить("ПараметрБазаДанных", Вкладка.БазаДанных + " / " + Вкладка.ИмяДанных + " / " + Вкладка.ПозицияДанных);
		Текст = Текст + Шаблон.ПолучитьОбласть("ОбластьВкладка", ПараметрыШаблона);
	КонецЦикла;
	// ПараметрыШаблона.Вставить("ПараметрВкладка", ИдВкладки);
	// Текст = Текст + Шаблон.ПолучитьОбласть("ОбластьНоваяВкладка", ПараметрыШаблона);
	ПараметрыШаблона = Новый Структура;
	ПараметрыШаблона.Вставить("ПараметрВкладки", Текст);
	ПараметрыШаблона.Вставить("ПараметрТекущаяВкладка", ТекущаяВкладка);
	ПараметрыШаблона.Вставить("ПараметрЗаголовокСтраницы", "" + УзелСвойство(ТекущиеДанные, "Заголовок") + " - OneScriptDB");
	ПараметрыШаблона.Вставить("ПараметрРежим", УзелСвойство(Вкладки.Получить(ТекущаяВкладка), "Режим"));
	Текст = Шаблон.ПолучитьОбласть("ОбластьВкладки", ПараметрыШаблона);
	Если ПараметрыШаблона.ПараметрРежим = "design" Тогда
		Текст = Текст + Шаблон.ПолучитьОбласть("ОбластьРежимРедактор");
	ИначеЕсли ПараметрыШаблона.ПараметрРежим = "view" Тогда
		Текст = Текст + Шаблон.ПолучитьОбласть("ОбластьРежимПросмотр");
	Иначе
		Текст = Текст + Шаблон.ПолучитьОбласть("ОбластьРежимСтруктура");
	КонецЕсли;
	// Меню пользователя
	ПараметрыШаблона = Новый Структура;
	ПараметрыШаблона.Вставить("ПараметрСубъект", Субъект);
	Текст = Текст + Шаблон.ПолучитьОбласть(?(Субъект = "Гость", "ОбластьГость", "ОбластьСубъект"), ПараметрыШаблона);
	Возврат Текст;
КонецФункции // ПоказатьВкладки()


Функция НоваяСтруктураВкладка(Данные, ТипВкладки, Заголовок, Режим, УзелКод, БазаДанных, ИмяДанных, ПозицияДанных)

	НоваяВкладка = Новый Структура("ИдВкладки, ТипВкладки, Данные, Состояния, Заголовок, Режим, ИдУзла, БазаДанных, ИмяДанных, ПозицияДанных, ОбновитьУзел, Прокрутка",
		ИдВкладки, ТипВкладки, Данные, Новый Соответствие, Заголовок, Режим, УзелКод, БазаДанных, ИмяДанных, ПозицияДанных, Истина, "0");
	Если НЕ ТекущаяВкладка = Неопределено Тогда
		ВкладкиСписок.Вставить(ВкладкиСписок.Индекс(ВкладкиСписок.НайтиПоЗначению(Вкладки.Получить(ТекущаяВкладка))) + 1, НоваяВкладка);
	Иначе
		ВкладкиСписок.Добавить(НоваяВкладка);
	КонецЕсли;
	Вкладки.Вставить(ИдВкладки, НоваяВкладка);
	ТекущаяВкладка = ИдВкладки;
	ИдВкладки = ИдВкладки + 1;
	ОбщийРезультат = ОбщийРезультат + "<div id='insert'><script>newtab('" + НоваяВкладка.ИдВкладки + "','" + НоваяВкладка.ТипВкладки + "','" + НоваяВкладка.Данные.Фронт.Код + "');</script></div>";
	ОбновитьВкладки = Истина;

	Возврат НоваяВкладка;

КонецФункции // НоваяСтруктураВкладка()


Функция ПолучитьДанные(БазаДанных, ИмяДанных, ПозицияДанных)
	ПутьДанные = БазаДанных + "/" + ИмяДанных + "/" + ПозицияДанных;
	Данные = ВсеДанные.Получить(ПутьДанные);
	Если Данные = Неопределено Тогда
		Если НЕ "" + БазаДанных = "" И НЕ "" + ПозицияДанных = "" Тогда
			Соединение = Неопределено;
			ПередатьСтроку(Соединение, "osdb" + Символы.Таб + БазаДанных + Символы.Таб + "dataposition" + Символы.Таб + ПозицияДанных);
			Попытка
				ПозицияДанных = Соединение.ПрочитатьСтроку();
				Соединение.Закрыть();
			Исключение
				Сообщить(ОписаниеОшибки());
				Возврат Неопределено;
			КонецПопытки;
		КонецЕсли;
		Данные = Новый pagedata(ЭтотОбъект, БазаДанных, ИмяДанных, ПозицияДанных);
		ВсеДанные.Вставить(ПутьДанные, Данные);
		// системное событие
		Если НЕ Настройки = Неопределено Тогда
			СистемныйУзел = Настройки.НайтиУзел(Настройки.Корень, "ПриОткрытииДанных");
			Если НЕ СистемныйУзел = Неопределено Тогда
				СобытиеУзел = Настройки.НовыйУзел(Новый Структура("Имя, Значение", "Строка", "" + ТекущаяДата()), Истина);
				СистемныйУзел.Вставить("сДочерний", СобытиеУзел.Код);
				Атр = Настройки.НовыйАтрибут(СобытиеУзел, Новый Структура("Имя, Значение", "ИмяДанных", Данные.ИмяДанных), Истина);
				Атр = Настройки.НовыйСоседний(Атр, Новый Структура("Имя, Значение", "ПозицияДанных", Данные.ПозицияДанных), Истина);
				Атр = Настройки.НовыйСоседний(Атр, Новый Структура("Имя, Значение", "БазаДанных", Данные.БазаДанных), Истина);
				// Зарегистрировать обновление связанных узлов
				Настройки.ОбновитьУзел(СистемныйУзел);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	Возврат Данные;
КонецФункции


Функция НоваяВкладка(Параметры) Экспорт
	Перем ИмяДанных;
	БазаДанных = "" + УзелСвойство(Параметры, "osdb");
	ИмяДанных = "" + УзелСвойство(Параметры, "data");
	ПозицияДанных = "" + УзелСвойство(Параметры, "dataposition");
	Режим = "" + УзелСвойство(Параметры, "mode");
	ТипВкладки = "data";
	Данные = ПолучитьДанные(БазаДанных, ИмяДанных, ПозицияДанных);
	//Данные.Обновить = Истина;
	Если Режим = "" Тогда
		Если Данные.Фронт = Данные.Корень Тогда
			Режим = "struct";
			ТипВкладки = "win";
		Иначе
			Режим = "view";
		КонецЕсли;
	КонецЕсли;
	ТекущиеДанные = НоваяСтруктураВкладка(Данные, ТипВкладки, ИмяДанных, Режим, Данные.Корень.Код, БазаДанных, ИмяДанных, ПозицияДанных);
	Возврат ТекущиеДанные;
КонецФункции


Функция НачальнаяСтраница()
	Текст = Шаблон.ПолучитьОбласть("ОбластьШапка", Новый Структура("ПараметрХост", ПараметрХост))
		+ Шаблон.ПолучитьОбласть("ОбластьПанельМенюОкно", Новый Структура("ПараметрХост", ПараметрХост))
		+ Шаблон.ПолучитьОбласть("ОбластьПодвал", Новый Структура("ПараметрИдПроцесса, ПараметрХост", procid, ПараметрХост));
	Возврат Текст;
КонецФункции

Функция ПоказатьСтраницу(ТекстСтраницы)
	Текст = Шаблон.ПолучитьОбласть("ОбластьШапка", Новый Структура("ПараметрХост", ПараметрХост))
		+ Шаблон.ПолучитьОбласть("ОбластьПоказатьСтраницу", Новый Структура("ПараметрХост, ПараметрТекстСтраницы", ПараметрХост, ТекстСтраницы));
	Возврат Текст;
КонецФункции


Функция СтрокуВСтруктуру(Знач Стр)
	Стр = СтрРазделить(Стр, Символы.Таб);
	Ключ = Неопределено;
	Рез = Неопределено;
	Для Каждого знСтр Из Стр Цикл
		Если Ключ = Неопределено Тогда
			Ключ = знСтр;
		Иначе
			Если Рез = Неопределено Тогда
				Рез = Новый Структура;
			КонецЕсли;
			Рез.Вставить(Ключ, знСтр);
			Ключ = Неопределено;
		КонецЕсли;
	КонецЦикла;
	Возврат Рез;
КонецФункции


Функция СтруктуруВСтроку(Структ)
	Результат = "";
	Для каждого Элемент Из Структ Цикл
		Результат = Результат + ?(Результат = "", "", Символы.Таб) + Элемент.Ключ + Символы.Таб + Элемент.Значение;
	КонецЦикла;
	Возврат Результат;
КонецФункции


Функция ПоказатьМенюИнструменты(Вкладка, Узел, ЭтоАтрибут = Ложь)

	ПараметрыШаблона = Новый Структура;
	ПараметрыШаблона.Вставить("ПараметрИдУзла", "" + Вкладка.ИдВкладки + "_" + Узел.Код);
	ПараметрыШаблона.Вставить("ПараметрЭтоАтрибут", Число(ЭтоАтрибут));
	ПараметрыШаблона.Вставить("ПараметрНеактивно", "");
	ПараметрыШаблона.Вставить("ПараметрВидимость", "");

	ПараметрМенюИнструменты = "";

	Если Вкладка.Режим = "design" Тогда

		ПараметрыШаблона.Вставить("ПараметрВкладка", ИдВкладки);
		ПараметрМенюИнструменты = ПараметрМенюИнструменты + Шаблон.ПолучитьОбласть("ОбластьМенюРедактировать", ПараметрыШаблона);

		// Если НЕ УзелСостояние(Вкладка, Узел, "Изменить") = Истина Тогда
		// 	ПараметрыШаблона.Вставить("ПараметрКоманда", Команда["УзелСтруктура"]);
		// 	ПараметрыШаблона.Вставить("ПараметрПодсказка", "Структура");
		// Иначе
		// 	ПараметрыШаблона.Вставить("ПараметрКоманда", Команда["УзелПросмотр"]);
		// 	ПараметрыШаблона.Вставить("ПараметрПодсказка", "Представление");
		// КонецЕсли;
		// ПараметрМенюИнструменты = ПараметрМенюИнструменты + Шаблон.ПолучитьОбласть("ОбластьМенюИнструменты", ПараметрыШаблона);

	Иначе

		ПараметрыШаблона.Вставить("ПараметрКоманда", Команда["РедактироватьУзел"]);
		ПараметрыШаблона.Вставить("ПараметрПодсказка", "Редактировать узел");
		ПараметрМенюИнструменты = ПараметрМенюИнструменты + Шаблон.ПолучитьОбласть("ОбластьМенюИнструменты", ПараметрыШаблона);

		ПараметрыШаблона.Вставить("ПараметрКоманда", Команда["ЗначениеУзла"]);
		ПараметрыШаблона.Вставить("ПараметрПодсказка", "Значение узла");
		ПараметрМенюИнструменты = ПараметрМенюИнструменты + Шаблон.ПолучитьОбласть("ОбластьМенюИнструменты", ПараметрыШаблона);

		// ПараметрыШаблона.Вставить("ПараметрКоманда", Команда["ЗначениеОбъекта"]);
		// ПараметрыШаблона.Вставить("ПараметрПодсказка", "Значение объекта");
		// ПараметрМенюИнструменты = ПараметрМенюИнструменты + Шаблон.ПолучитьОбласть("ОбластьМенюИнструменты", ПараметрыШаблона);

		Если НЕ этоАтрибут = Истина Тогда
			ПараметрыШаблона.Вставить("ПараметрКоманда", Команда["НовыйРодитель"]);
			ПараметрыШаблона.Вставить("ПараметрПодсказка", "Новый родитель");
			ПараметрМенюИнструменты = ПараметрМенюИнструменты + Шаблон.ПолучитьОбласть("ОбластьМенюИнструменты", ПараметрыШаблона);

			ПараметрыШаблона.Вставить("ПараметрКоманда", Команда["НовыйАтрибут"]);
			ПараметрыШаблона.Вставить("ПараметрПодсказка", "Новый атрибут");
			ПараметрМенюИнструменты = ПараметрМенюИнструменты + Шаблон.ПолучитьОбласть("ОбластьМенюИнструменты", ПараметрыШаблона);

			ПараметрыШаблона.Вставить("ПараметрКоманда", Команда["НовыйДочерний"]);
			ПараметрыШаблона.Вставить("ПараметрПодсказка", "Новый дочерний");
			ПараметрМенюИнструменты = ПараметрМенюИнструменты + Шаблон.ПолучитьОбласть("ОбластьМенюИнструменты", ПараметрыШаблона);
		КонецЕсли;

		ПараметрыШаблона.Вставить("ПараметрКоманда", Команда["НовыйСоседний"]);
		ПараметрыШаблона.Вставить("ПараметрПодсказка", "Новый соседний");
		ПараметрМенюИнструменты = ПараметрМенюИнструменты + Шаблон.ПолучитьОбласть("ОбластьМенюИнструменты", ПараметрыШаблона);

	КонецЕсли;

	ПараметрыШаблона.Вставить("ПараметрКоманда", Команда["ВырезатьУзел"]);
	ПараметрыШаблона.Вставить("ПараметрПодсказка", "Вырезать узел");
	ПараметрМенюИнструменты = ПараметрМенюИнструменты + Шаблон.ПолучитьОбласть("ОбластьМенюИнструменты", ПараметрыШаблона);

	ПараметрыШаблона.Вставить("ПараметрКоманда", Команда["КопироватьУзел"]);
	ПараметрыШаблона.Вставить("ПараметрПодсказка", "Копировать узел");
	ПараметрМенюИнструменты = ПараметрМенюИнструменты + Шаблон.ПолучитьОбласть("ОбластьМенюИнструменты", ПараметрыШаблона);

	Если НЕ этоАтрибут = Истина Тогда
		ПараметрыШаблона.Вставить("ПараметрКоманда", Команда["ВставитьАтрибут"]);
		ПараметрыШаблона.Вставить("ПараметрПодсказка", "Вставить атрибут");
		ПараметрМенюИнструменты = ПараметрМенюИнструменты + Шаблон.ПолучитьОбласть("ОбластьМенюИнструменты", ПараметрыШаблона);
	КонецЕсли;

	ПараметрыШаблона.Вставить("ПараметрКоманда", Команда["ВставитьДочерний"]);
	ПараметрыШаблона.Вставить("ПараметрПодсказка", "Вставить дочерний");
	ПараметрМенюИнструменты = ПараметрМенюИнструменты + Шаблон.ПолучитьОбласть("ОбластьМенюИнструменты", ПараметрыШаблона);

	ПараметрыШаблона.Вставить("ПараметрКоманда", Команда["ВставитьСоседний"]);
	ПараметрыШаблона.Вставить("ПараметрПодсказка", "Вставить соседний");
	ПараметрМенюИнструменты = ПараметрМенюИнструменты + Шаблон.ПолучитьОбласть("ОбластьМенюИнструменты", ПараметрыШаблона);

	Если НЕ этоАтрибут = Истина Тогда
		ПараметрыШаблона.Вставить("ПараметрКоманда", Команда["УдалитьАтрибуты"]);
		ПараметрыШаблона.Вставить("ПараметрПодсказка", "Удалить все атрибуты");
		ПараметрМенюИнструменты = ПараметрМенюИнструменты + Шаблон.ПолучитьОбласть("ОбластьМенюИнструменты", ПараметрыШаблона);

		ПараметрыШаблона.Вставить("ПараметрКоманда", Команда["УдалитьРодителя"]);
		ПараметрыШаблона.Вставить("ПараметрПодсказка", "Удалить родителя");
		ПараметрМенюИнструменты = ПараметрМенюИнструменты + Шаблон.ПолучитьОбласть("ОбластьМенюИнструменты", ПараметрыШаблона);
	КонецЕсли;

	ПараметрыШаблона.Вставить("ПараметрКоманда", Команда["УдалитьУзел"]);
	ПараметрыШаблона.Вставить("ПараметрПодсказка", "Удалить узел");
	ПараметрМенюИнструменты = ПараметрМенюИнструменты + Шаблон.ПолучитьОбласть("ОбластьМенюИнструменты", ПараметрыШаблона);

	ПараметрыШаблона = Новый Структура;
	ПараметрыШаблона.Вставить("ПараметрВидМеню", Команда["МенюРедактора"]);
	ПараметрыШаблона.Вставить("ПараметрМенюИнструменты", ПараметрМенюИнструменты);
	КнопкаИнструменты = Шаблон.ПолучитьОбласть("ОбластьМеню", ПараметрыШаблона);

	Возврат КнопкаИнструменты;

КонецФункции // ПоказатьМенюИнструменты()


Функция ПоказатьСтруктуруУзла(Вкладка, Узел, ЭтоАтрибут = Ложь, Атрибуты = "", Дочерний = "") Экспорт

	Представление = "" ;

	УзелОткрыт = УзелСостояние(Вкладка, Узел, "УзелОткрыт");
	Если УзелОткрыт = Неопределено Тогда
		УзелОткрыт = Ложь;
	КонецЕсли;

	УзелИмя = Узел.Имя;

	УзелЗначение = "";
	Если Узел.Свойство("Значение") Тогда
		УзелЗначение = Узел.Значение;
	КонецЕсли;

	РедактироватьЗначение = УзелСостояние(Вкладка, Узел, "РедактироватьЗначение");
	РедактироватьИмя = УзелСостояние(Вкладка, Узел, "РедактироватьИмя");

	Если этоАтрибут = Истина Тогда
		УзелИмя = СтрЗаменить(УзелИмя, "xml_lang", "xml:lang");
		УзелИмя = СтрЗаменить(УзелИмя, "_", "-");
	КонецЕсли;

	КнопкаУзел = "";

	Если НЕ этоАтрибут = Истина Тогда
		ПараметрыШаблона = Новый Структура;
		ПараметрыШаблона.Вставить("ПараметрИдУзла", "" + Вкладка.ИдВкладки + "_" + Узел.Код);
		ПараметрыШаблона.Вставить("ПараметрКоманда", ?(УзелОткрыт, Команда["ЗакрытьУзел"], Команда["ОткрытьУзел"]));
		ПараметрыШаблона.Вставить("ПараметрНадписьНаКнопке", ?(УзелОткрыт ИЛИ УзелСвойство(Узел, "Дочерний") = Неопределено, "⚪" , "⚫"));
		КнопкаУзел = Шаблон.ПолучитьОбласть("ОбластьКнопкаУзел", ПараметрыШаблона);
	Иначе
		ПараметрыШаблона = Новый Структура;
		ПараметрыШаблона.Вставить("ПараметрИдУзла", "" + Вкладка.ИдВкладки + "_" + Узел.Код);
		ПараметрыШаблона.Вставить("ПараметрКоманда", Команда["РедактироватьУзел"]);
		ПараметрыШаблона.Вставить("ПараметрНадписьНаКнопке", ?(УзелСвойство(Узел, "Дочерний") = Неопределено, "⚪" , "⚫"));
		КнопкаУзел = Шаблон.ПолучитьОбласть("ОбластьКнопкаУзел", ПараметрыШаблона);
	КонецЕсли;

	Если РедактироватьИмя = Истина Тогда

		ПараметрыШаблона = Новый Структура;
		ПараметрыШаблона.Вставить("ПараметрИдУзла", "" + Вкладка.ИдВкладки + "_" + Узел.Код);
		ПараметрыШаблона.Вставить("ПараметрКоманда", Команда["НовоеИмяУзла"]);
		ПараметрыШаблона.Вставить("ПараметрИмяЗначение", КодироватьСтроку(УзелИмя, СпособКодированияСтроки.КодировкаURL));
		//ПараметрыШаблона.Вставить("ПараметрДлинаСтроки", РазмерПоля(УзелИмя));
		ПараметрыШаблона.Вставить("ПараметрЭтоАтрибут", Число(ЭтоАтрибут));
		ПараметрИмя = Шаблон.ПолучитьОбласть("ОбластьИзменитьИмяЗначение", ПараметрыШаблона);

	Иначе

		ПараметрыШаблона = Новый Структура;
		ПараметрыШаблона.Вставить("ПараметрИдУзла", Узел.Код);
		ПараметрыШаблона.Вставить("ПараметрИдВкладки", "" + Вкладка.ИдВкладки);
		ПараметрыШаблона.Вставить("ПараметрКоманда", Команда["МенюРедактора"]);
		ПараметрыШаблона.Вставить("ПараметрНадписьНаКнопке", СтрЭкранироватьРазметку(УзелИмя, 50));
		ПараметрыШаблона.Вставить("ПараметрЭтоАтрибут", Число(ЭтоАтрибут));
		ПараметрыШаблона.Вставить("ПараметрСистемныйУзел", ?(Лев(Узел.Код, 1) = "s", "-s", ""));
		ПараметрИмя = Шаблон.ПолучитьОбласть("ОбластьКнопкаИмяЗначение", ПараметрыШаблона);

	КонецЕсли;

	Если РедактироватьЗначение = Истина Тогда

		ПараметрыШаблона = Новый Структура;
		ПараметрыШаблона.Вставить("ПараметрИдУзла", "" + Вкладка.ИдВкладки + "_" + Узел.Код);
		ПараметрыШаблона.Вставить("ПараметрКоманда", Команда["НовоеЗначениеУзла"]);
		ПараметрыШаблона.Вставить("ПараметрИмяЗначение", КодироватьСтроку(УзелЗначение, СпособКодированияСтроки.КодировкаURL));
		//ПараметрыШаблона.Вставить("ПараметрДлинаСтроки", РазмерПоля(УзелЗначение));
		ПараметрыШаблона.Вставить("ПараметрЭтоАтрибут", Число(ЭтоАтрибут));
		ПараметрЗначение = Шаблон.ПолучитьОбласть("ОбластьИзменитьИмяЗначение", ПараметрыШаблона);

	Иначе

		ПараметрыШаблона = Новый Структура;
		ПараметрыШаблона.Вставить("ПараметрИдУзла", Узел.Код);
		ПараметрыШаблона.Вставить("ПараметрИдВкладки", "" + Вкладка.ИдВкладки);
		ПараметрыШаблона.Вставить("ПараметрКоманда", Команда["РедактироватьЗначение"]);
		ПараметрыШаблона.Вставить("ПараметрНадписьНаКнопке", СтрЭкранироватьРазметку(УзелЗначение, 100));
		ПараметрыШаблона.Вставить("ПараметрЭтоАтрибут", Число(ЭтоАтрибут));
		ПараметрыШаблона.Вставить("ПараметрСистемныйУзел", ?(Лев(Узел.Код, 1) = "s", "-s", ""));
		ПараметрЗначение = Шаблон.ПолучитьОбласть("ОбластьКнопкаИмяЗначение", ПараметрыШаблона);

	КонецЕсли;

	ПараметрыШаблона = Новый Структура;
	ПараметрыШаблона.Вставить("ПараметрИдУзла", "" + Вкладка.ИдВкладки + "_" + Узел.Код);
	ПараметрыШаблона.Вставить("ПараметрИмя", ПараметрИмя);
	ПараметрыШаблона.Вставить("ПараметрЗначение", ПараметрЗначение);
	ПараметрИмяУзла = КнопкаУзел + Шаблон.ПолучитьОбласть("ОбластьИмяЗначение", ПараметрыШаблона);

	Если НЕ ЭтоАтрибут Тогда

		ПараметрЗаголовокУзла = ПараметрИмяУзла + Атрибуты;

		ПараметрДочернийУзел = Дочерний;

		ПараметрыШаблона = Новый Структура;
		ПараметрыШаблона.Вставить("ПараметрИдУзла", "" + Вкладка.ИдВкладки + "_" + Узел.Код);
		ПараметрыШаблона.Вставить("ПараметрЗаголовокУзла", ПараметрЗаголовокУзла);
		ПараметрыШаблона.Вставить("ПараметрДочернийУзел", ПараметрДочернийУзел);
		Представление = Шаблон.ПолучитьОбласть("ОбластьУзел", ПараметрыШаблона);

	Иначе

		Представление = ПараметрИмяУзла;

	КонецЕсли;

	Если УзелСостояние(Вкладка, Узел, "РедактироватьУзел") = Истина Тогда
		ПараметрыШаблона = Новый Структура;
		ПараметрыШаблона.Вставить("ПараметрВкладка", "" + Вкладка.ИдВкладки);
		ПараметрыШаблона.Вставить("ПараметрИдУзла", "" + Вкладка.ИдВкладки + "_" + Узел.Код);
		ПараметрыШаблона.Вставить("ПараметрИмяУзла", СтрЭкранироватьРазметку(УзелИмя));
		ПараметрыШаблона.Вставить("ПараметрЗначениеУзла", КодироватьСтроку(УзелЗначение, СпособКодированияСтроки.КодировкаURL));
		ПараметрыШаблона.Вставить("ПараметрЭтоАтрибут", Число(ЭтоАтрибут));
		Представление = Представление + Шаблон.ПолучитьОбласть("ОбластьРедактироватьУзел", ПараметрыШаблона);
	КонецЕсли;

	Если УзелСостояние(Вкладка, Узел, "ЗагрузитьHTML") = Истина Тогда
		ПараметрыШаблона = Новый Структура;
		ПараметрыШаблона.Вставить("ПараметрВкладка", "" + Вкладка.ИдВкладки);
		ПараметрыШаблона.Вставить("ПараметрИдУзла", "" + Вкладка.ИдВкладки + "_" + Узел.Код);
		ПараметрыШаблона.Вставить("ПараметрЭтоАтрибут", Число(ЭтоАтрибут));
		ПараметрыШаблона.Вставить("ПараметрХост", ПараметрХост);
		Представление = Представление + Шаблон.ПолучитьОбласть("ОбластьЗагрузитьHTML", ПараметрыШаблона);
	КонецЕсли;

	Возврат Представление;

КонецФункции


Функция ОтобразитьDOM(Вкладка, Узел, Родитель = Неопределено, Знач Обновить = Ложь, Знач ТипУзла = "", Знач НачальныйУзел = Ложь)

	Данные = Вкладка.Данные;

	Представление = Неопределено;

	Если УзелСостояние(Вкладка, Узел, "НачальныйУзел") = Истина Тогда
		НачальныйУзел = Истина;
	КонецЕсли;
	Если УзелСостояние(Вкладка, Узел, "ОбновитьУзел") = Истина Тогда
		Обновить = Истина;
	Иначе
		Представление = УзелСостояние(Вкладка, Узел, "Представление");
		Если НЕ Представление = Неопределено И НЕ Обновить Тогда
			Представление = "";
		КонецЕсли;
	КонецЕсли;

	Если Представление = Неопределено Тогда
		Если Обновить Тогда
			Атрибуты = "";
			УзелАтрибут = Данные.Атрибут(Узел);
			Если НЕ УзелАтрибут = Неопределено Тогда
				Атрибуты = ОтобразитьDOM(Вкладка, УзелАтрибут, Узел, Обновить, "Атрибут");
			КонецЕсли;

			Дочерний = "";
			Если УзелСостояние(Вкладка, Узел, "УзелОткрыт") = Истина  Тогда
				УзелДочерний = Данные.Дочерний(Узел);
				Если НЕ УзелДочерний = Неопределено Тогда
					Дочерний = ОтобразитьDOM(Вкладка, УзелДочерний, Узел, Обновить, "Дочерний");
				КонецЕсли;
			КонецЕсли;

			Представление = ПоказатьСтруктуруУзла(Вкладка, Узел, (ТипУзла = "Атрибут"), Атрибуты, Дочерний);
			УзелСостояниеЗначение(Вкладка, Узел, "Представление", Представление);
			УзелСостояниеЗначение(Вкладка, Узел, "ОбновитьУзел", Ложь);
			УзелСостояниеЗначение(Вкладка, Узел, "Родитель", Родитель);
		Иначе
			Если УзелСостояние(Вкладка, Узел, "УзелОткрыт") = Истина  Тогда
				УзелДочерний = Данные.Дочерний(Узел);
				Если НЕ УзелДочерний = Неопределено Тогда
					Представление = ОтобразитьDOM(Вкладка, УзелДочерний, Узел, Обновить, "Дочерний");
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

	Если НЕ НачальныйУзел Тогда
		УзелСоседний = Данные.Соседний(Узел);
		Если НЕ УзелСоседний = Неопределено Тогда
			Соседний = ОтобразитьDOM(Вкладка, УзелСоседний, Узел, Обновить, ТипУзла);
			Представление = "" + Представление + Соседний;
		КонецЕсли;
	КонецЕсли;

	Возврат "" + Представление;

КонецФункции // ОтобразитьDOM()


Функция ОбработатьКоманду(структЗадача) Экспорт

	Запрос = структЗадача.Запрос;

	ДействиеИмя = УзелСвойство(Запрос, "cmd");

	Если НЕ ДействиеИмя = Неопределено Тогда

		структЗадача.Действие = Новый Структура("Имя, Результат", ДействиеИмя);
		Возврат Истина;

	КонецЕсли;

	Возврат Ложь;

КонецФункции // ОбработатьКоманду()


Функция ЗапросВыполнитьЗадачу(структЗадача)
	Перем domupdate;

	Сообщить("ЗапросВыполнитьЗадачу="+структЗадача.Запрос.taskid+" этап=" + структЗадача.Этап);

	Если структЗадача.Этап = "ВыполнитьЗадачу" Тогда

		структЗадача.Вставить("ВремяНачало", ТекущаяДата());
		структЗадача.Вставить("Команд", 0);

		Если структЗадача.Запрос.Свойство("domupdate", domupdate) Тогда
			Если Число(domupdate) > НомерОбновления Тогда
				// ждем события
			Иначе
				Если domupdate = "0" Тогда
					Если НЕ Вкладки.Количество() Тогда
						НоваяВкладка(Новый Структура("data", ИмяДанных));
					КонецЕсли;
					Скрипт = "";
					Для каждого элВкладка Из Вкладки Цикл
						Вкладка = элВкладка.Значение;
						Вкладка.ОбновитьУзел = Истина;
						//Вкладка.Данные.Обновить = Истина;
						Скрипт = Скрипт + "newtab('" + Вкладка.ИдВкладки + "','" + Вкладка.ТипВкладки + "','" + Вкладка.Данные.Фронт.Код + "');";
					КонецЦикла;
					структЗадача.Результат = структЗадача.Результат + "<div id='insert'><script>" + Скрипт + "</script></div>";
					ОбновитьВкладки = Истина;
				КонецЕсли;
				структЗадача.Этап = "СформироватьОтвет";
			КонецЕсли;
			НомерОбновления = Число(domupdate);
		ИначеЕсли ОбработатьКоманду(структЗадача) Тогда
			структЗадача.Этап = "ВыполнитьДействия";
		Иначе
			ИмяДанных = "" + УзелСвойство(структЗадача.Запрос, "data");
			Если Гость Тогда
				Если ИмяДанных = "" Тогда
					ИмяДанных = "start";
				КонецЕсли;
				Данные = ПолучитьДанные("", ИмяДанных, "");
				Команд = Данные.К;
				Данные.ОбновитьПредставление();
				Команд = Данные.К - Команд;
				Сообщить("Команд " + Команд);
				структЗадача.Результат = ПоказатьСтраницу(Данные.Представление);
			Иначе
				Если ИмяДанных = "" Тогда
					ИмяДанных = "expldb";
				КонецЕсли;
				структЗадача.Результат = НачальнаяСтраница();
			КонецЕсли;
			структЗадача.Этап = "ЕстьРезультат";
		КонецЕсли;

	КонецЕсли;

	Если структЗадача.Этап = "ВыполнитьДействия" Тогда
		Если ВыполнитьДействия(структЗадача) = Истина Тогда
		КонецЕсли;
		структЗадача.Этап = "УдалитьЗадачу";
	КонецЕсли;

	Если структЗадача.Этап = "СформироватьОтвет" Тогда

		структЗадача.Результат = структЗадача.Результат + ОбщийРезультат;
		ОбщийРезультат = "";

		Для каждого ЭлДанные Из ВсеДанные Цикл
			текДанные = ЭлДанные.Значение;
			Команд = текДанные.К;
			текДанные.ОбновитьПредставление();
			Команд = текДанные.К - Команд;
			структЗадача.Вставить("Команд", структЗадача.Команд + Команд);
		КонецЦикла;

		Для каждого элВкладка Из Вкладки Цикл
			Вкладка = элВкладка.Значение;
			Если Вкладка.ОбновитьУзел = Истина И Вкладка.Режим = "view" Тогда
				Ответ = "" + СтрЗаменить(Вкладка.Данные.Представление, "id=""_", "id=""" + Вкладка.ИдВкладки + "_");
			ИначеЕсли Вкладка.Режим = "view" ИЛИ Вкладка.Режим = "design" Тогда
				Ответ = "" + СтрЗаменить(Вкладка.Данные.Обновление, "id=""_", "id=""" + Вкладка.ИдВкладки + "_");
			ИначеЕсли Вкладка.Режим = "struct" Тогда
				Ответ = "" + ОтобразитьDOM(Вкладка, Вкладка.Данные.ПолучитьУзел(Вкладка.ИдУзла), , Вкладка.ОбновитьУзел, , Истина);
			КонецЕсли;
			Если Вкладка.ТипВкладки = "win" Тогда
				Если Вкладка.ОбновитьУзел = Истина Тогда
					ПараметрыШаблона = Новый Структура;
					ПараметрыШаблона.Вставить("ПараметрЗаголовокОкна", Вкладка.Заголовок);
					ПараметрыШаблона.Вставить("ПараметрСодержимоеОкна", Ответ);
					ПараметрыШаблона.Вставить("ПараметрВкладка", Вкладка.ИдВкладки);
					ПараметрыШаблона.Вставить("ПараметрИдУзла", Вкладка.ИдУзла);
					Ответ = Шаблон.ПолучитьОбласть("ОбластьПанель", ПараметрыШаблона);
				КонецЕсли;
			КонецЕсли;
			структЗадача.Результат = структЗадача.Результат + Ответ;
			Если Вкладка.ОбновитьУзел И (Вкладка = ТекущиеДанные ИЛИ Вкладка = ТекущееОкно) Тогда
				ПараметрыШаблона = Новый Структура;
				ПараметрыШаблона.Вставить("ПараметрТипВкладки", Вкладка.ТипВкладки);
				ПараметрыШаблона.Вставить("ПараметрПрокрутка", Вкладка.Прокрутка);
				ПараметрыШаблона.Вставить("ПараметрИдВкладки", Вкладка.ИдВкладки);
				ПараметрыШаблона.Вставить("ПараметрРежим", Вкладка.Режим);
				ПараметрыШаблона.Вставить("ПараметрТекущаяВкладка", ТекущаяВкладка);
				структЗадача.Результат = структЗадача.Результат + Шаблон.ПолучитьОбласть("ОбластьСкрипт", ПараметрыШаблона);
			КонецЕсли;
			Вкладка.ОбновитьУзел = Ложь;
		КонецЦикла;

		Если ОбновитьВкладки = Истина Тогда
			структЗадача.Результат = структЗадача.Результат + ПоказатьВкладки();
			ОбновитьВкладки = Ложь;
		КонецЕсли;

		Если НЕ структЗадача.Результат = "" Тогда
			структЗадача.Результат = структЗадача.Результат + Шаблон.ПолучитьОбласть("ОбластьСтатус", Новый Структура("ПараметрСтатусСообщение", "" + Цел(100*(ТекущаяДата() - структЗадача.ВремяНачало))/100 + " с. " + структЗадача.Команд + " к."));
			структЗадача.Этап = "ЕстьРезультат";
		Иначе
			структЗадача.Этап = "ВыполнитьЗадачу";
		КонецЕсли;
	КонецЕсли;

	Если структЗадача.Этап = "ЕстьРезультат" Тогда
		Соединение = Неопределено;
		Если ПередатьСтроку(Соединение, "<!--" + структЗадача.Запрос.taskid + "-->" + структЗадача.Результат + "<!--end-->") Тогда
			Если НЕ Соединение = Неопределено Тогда
				Соединение.Закрыть();
				структЗадача.Этап = "УдалитьЗадачу";
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

КонецФункции // ЗапросВыполнитьЗадачу()


Функция УзелСвойство(Узел, Свойство) Экспорт
	УзелСвойство = Неопределено;
	Если НЕ Узел = Неопределено Тогда
		Узел.Свойство(Свойство, УзелСвойство);
	КонецЕсли;
	Возврат УзелСвойство;
КонецФункции // УзелСвойство(Узел)


Функция УзелСвойствоЗначение(Узел, СвойствоИмя, СвойствоЗначение) Экспорт
	Если НЕ Узел = Неопределено Тогда
		Узел.Вставить(СвойствоИмя, СвойствоЗначение);
	КонецЕсли;
	Возврат Неопределено;
КонецФункции // УзелСвойствоЗначение(Узел)


Функция УзелСостояние(Вкладка, Узел, СостояниеИмя) Экспорт
	УзелСостояние = Вкладка.Состояния.Получить(Узел.Код);
	Если НЕ УзелСостояние = Неопределено Тогда
		УзелСостояние.Свойство(СостояниеИмя, УзелСостояние);
	КонецЕсли;
	Возврат УзелСостояние;
КонецФункции // УзелСостояние(Узел)


Функция УзелСостояниеЗначение(Вкладка, Узел, СостояниеИмя, СостояниеЗначение, Событие = Ложь) Экспорт
	УзелСостояние = Вкладка.Состояния.Получить(Узел.Код);
	Если УзелСостояние = Неопределено Тогда
		УзелСостояние = Новый Структура();
		Вкладка.Состояния.Вставить(Узел.Код, УзелСостояние);
	КонецЕсли;
	УзелСостояние.Вставить(СостояниеИмя, СостояниеЗначение);
	//Сообщить("" + Вкладка.ИдВкладки + "_" + Узел.Код + " " + СостояниеИмя + "=" + (Лев(СостояниеЗначение,30)));
	Если Событие Тогда
		Если СостояниеИмя = "ОбновитьУзел" И СостояниеЗначение = Истина Тогда
			Вкладка.Данные.ОбновитьУзел(Узел);
		КонецЕсли;
	КонецЕсли;
	Возврат Неопределено;
КонецФункции // УзелСостояниеЗначение(Узел)


Функция ОбновитьСостояние(Вкладка, Узел, Состояние, Значение, Событие = Ложь, НачальныйУзел = Ложь)

	Если Узел = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;

	Данные = Вкладка.Данные;

	Результат = Истина;

	Если Состояние = "Изменить" Тогда
		// ОбновитьСостояние(Вкладка, Данные.Атрибут(Узел), "Изменить", Значение);
		// ОбновитьСостояние(Вкладка, Данные.Дочерний(Узел), "Изменить", Значение);
		// Если НЕ НачальныйУзел Тогда
		// 	ОбновитьСостояние(Вкладка, Данные.Соседний(Узел), "Изменить", Значение);
		// КонецЕсли;

	ИначеЕсли Состояние = "НовоеЗначениеУзла" Тогда
		ОбновитьСостояние(Вкладка, Узел, "РедактироватьЗначение", Ложь, Истина);
		УзелСвойствоЗначение(Узел, "Значение", Значение);

	ИначеЕсли Состояние = "НовоеИмяУзла" Тогда
		ОбновитьСостояние(Вкладка, Узел, "РедактироватьИмя", Ложь, Истина);
		ОбновитьСостояние(Вкладка, Узел, "РедактироватьЗначение", Истина, Истина);
		УзелСвойствоЗначение(Узел, "Имя", Значение);

	ИначеЕсли Состояние = "ОбновитьУзел" И Значение = Истина Тогда
		ОбновитьСостояние(Вкладка, Узел, "Представление", Неопределено, Ложь);

	ИначеЕсли Состояние = "Представление" И Значение = Неопределено Тогда
		Родитель = УзелСостояние(Вкладка, Узел, "Родитель");
		ОбновитьСостояние(Вкладка, Родитель, "Представление", Значение, Событие);

	КонецЕсли;

	Если НЕ Результат = Ложь Тогда
		УзелСостояниеЗначение(Вкладка, Узел, Состояние, Значение, Событие);
		Если НЕ Состояние = "ОбновитьУзел" И НЕ Состояние = "Представление" Тогда
			ОбновитьСостояние(Вкладка, Узел, "ОбновитьУзел", Истина, Событие);
		КонецЕсли;
	КонецЕсли;

	Возврат Результат;

КонецФункции // ОбновитьСостояние()


// Получить значение внешнего узла
Функция ВнешнийУзел(Знач Узел, Аргументы, Объект) Экспорт
	БазаДанных = "" + УзелСвойство(Аргументы, "БазаДанных");
	ИмяДанных = "" + УзелСвойство(Аргументы, "ИмяДанных");
	ПозицияДанных = "" + УзелСвойство(Аргументы, "ПозицияДанных");
	Данные = ПолучитьДанные(БазаДанных, ИмяДанных, ПозицияДанных);
	Возврат Данные.ВнешнийЗначение(Узел, Объект);
КонецФункции


Функция ВыполнитьДействия(структЗадача)
	Перем Вкладка;

	Действие = УзелСвойство(структЗадача, "Действие");
	Если НЕ Действие = Неопределено Тогда

		Запрос = структЗадача.Запрос;

		Если Действие.Имя = Команда["ЗавершитьПроцесс"] Тогда
			Сообщить("Получена команда на завершение.");
			ЗавершитьПроцесс = Истина;
			Возврат Истина;

		ИначеЕсли Действие.Имя = Команда["СохранитьДанные"] Тогда
			стрСообщение = "Неизвестно";
			Вкладка = Вкладки.Получить(ТекущаяВкладка);
			Если НЕ Вкладка = Неопределено Тогда
				Если "" + Вкладка.БазаДанных = "" Тогда
					Вкладка.Данные.СохранитьДанные(ОбъединитьПути(ТекущийКаталог(), "data", ".files", Вкладка.ИмяДанных));
					стрСообщение = "Данные сохранены " + Вкладка.ИмяДанных;
				Иначе
					datafile = "" + procid + ".s";
					Вкладка.Данные.СохранитьДанные(ОбъединитьПути(ТекущийКаталог(), "data", Вкладка.БазаДанных + ".files", datafile));
					Соединение = Неопределено;
					Если ПередатьСтроку(Соединение, "osdb" + Символы.Таб + Вкладка.БазаДанных + Символы.Таб + "data" + Символы.Таб + Вкладка.ИмяДанных + Символы.Таб + "datafile" + Символы.Таб + datafile) Тогда
						Попытка
							ФайлИмя = Соединение.ПрочитатьСтроку();
							Соединение.Закрыть();
							стрСообщение = "Данные сохранены";
						Исключение
							стрСообщение = ОписаниеОшибки();
						КонецПопытки;
					КонецЕсли;
				КонецЕсли
			КонецЕсли;
			ЗаписатьСобытие(Вкладка.БазаДанных + "/" + Вкладка.ИмяДанных, стрСообщение, 1);
			// системное событие
			Если НЕ Настройки = Неопределено Тогда
				СистемныйУзел = Настройки.НайтиУзел(Настройки.Корень, "ПриЗаписиДанных");
				Если НЕ СистемныйУзел = Неопределено Тогда
					СобытиеУзел = Настройки.НовыйУзел(Новый Структура("Имя, Значение", "Строка", "" + ТекущаяДата()), Истина);
					СистемныйУзел.Вставить("сДочерний", СобытиеУзел.Код);
					Атр = Настройки.НовыйАтрибут(СобытиеУзел, Новый Структура("Имя, Значение", "ИмяДанных", Вкладка.Данные.ИмяДанных), Истина);
					Атр = Настройки.НовыйСоседний(Атр, Новый Структура("Имя, Значение", "БазаДанных", Вкладка.Данные.БазаДанных), Истина);
					// Зарегистрировать обновление связанных узлов
					Настройки.ОбновитьУзел(СистемныйУзел);
				КонецЕсли;
			КонецЕсли;
			Возврат Истина;
		КонецЕсли;

		datascrolled = "" + УзелСвойство(Запрос, "datascrolled");
		Если НЕ datascrolled = "" И НЕ datascrolled = "undefined" Тогда
			Если НЕ ТекущиеДанные = Неопределено Тогда
				ТекущиеДанные.Вставить("Прокрутка", datascrolled);
			КонецЕсли;
		КонецЕсли;

		winscrolled = "" + УзелСвойство(Запрос, "winscrolled");
		Если НЕ winscrolled = "" И НЕ winscrolled = "undefined" Тогда
			Если НЕ ТекущееОкно = Неопределено Тогда
				ТекущееОкно.Вставить("Прокрутка", winscrolled);
			КонецЕсли;
		КонецЕсли;

		Если Действие.Имя = Команда["НоваяВкладка"] Тогда
			НоваяВкладка(Запрос);
			Возврат Истина;
		КонецЕсли;

		Если Действие.Имя = Команда["РежимРедактор"] Тогда
			Если ТекущиеДанные = Неопределено Тогда
				Возврат Ложь;
			КонецЕсли;
			ТекущиеДанные.Режим = "design";
			ОбщийРезультат = ОбщийРезультат + Шаблон.ПолучитьОбласть("ОбластьРежимРедактор");
			ОбновитьВкладки = Истина;
			Возврат Истина;
		КонецЕсли;

		Если Действие.Имя = Команда["РежимПросмотр"] Тогда
			Если ТекущиеДанные = Неопределено Тогда
				Возврат Ложь;
			КонецЕсли;
			ТекущиеДанные.Режим = "view";
			ОбщийРезультат = ОбщийРезультат + Шаблон.ПолучитьОбласть("ОбластьРежимПросмотр");
			ОбновитьВкладки = Истина;
			Возврат Истина;
		КонецЕсли;

		tab = "" + УзелСвойство(Запрос, "tab");
		Если НЕ tab = "" И НЕ tab = "undefined" Тогда
			tab = Число(tab);
			Вкладка = Вкладки.Получить(tab);
			Если Вкладка = Неопределено Тогда
				Возврат Ложь;
			КонецЕсли;

			Если Действие.Имя = Команда["ЗакрытьВкладку"] Тогда
				ОбщийРезультат = ОбщийРезультат + "<div id='" + tab + "_0'/>";
				Если Вкладка = ТекущиеДанные Тогда
					ТекущиеДанные = Неопределено;
				ИначеЕсли Вкладка = ТекущееОкно Тогда
					ТекущееОкно = Неопределено;
				КонецЕсли;

				Вкладки.Удалить(tab);
				удВкладка = Вкладка;
				ВкладкиСписок.Удалить(ВкладкиСписок.НайтиПоЗначению(удВкладка));

				Если tab = ТекущаяВкладка Тогда
					Если ТекущиеДанные = Неопределено Тогда
						ТекущаяВкладка = Неопределено;
						Для каждого элВкладка Из ВкладкиСписок Цикл
							знВкладка = элВкладка.Значение;
							Если знВкладка = удВкладка Тогда
								tab = Неопределено;
								Продолжить;
							КонецЕсли;
							Если tab = Неопределено И НЕ ТекущаяВкладка = Неопределено Тогда
								Прервать;
							КонецЕсли;
							Если знВкладка.ТипВкладки = "data" Тогда
								Вкладка = знВкладка;
								ТекущаяВкладка = Вкладка.ИдВкладки;
								ТекущиеДанные = Вкладка;
								Вкладка.ОбновитьУзел = Истина;
							КонецЕсли;
						КонецЦикла;
					Иначе
						ТекущаяВкладка = ТекущиеДанные.ИдВкладки;
					КонецЕсли;
				КонецЕсли;

				ОбновитьВкладки = Истина;
				Возврат Истина;
			КонецЕсли;

			Если Действие.Имя = Команда["ВыбратьВкладку"] Тогда
				ТекущаяВкладка = tab;
				Если Вкладка.ТипВкладки = "data" Тогда
					ТекущиеДанные = Вкладка;
					ТекущееОкно = Неопределено;
				КонецЕсли;
				Если Вкладка.ТипВкладки = "win" Тогда
					ТекущееОкно = Вкладка;
				КонецЕсли;
				//Вкладка.ОбновитьУзел = Истина;
				ОбновитьВкладки = Истина;
				Возврат Истина;
			КонецЕсли;

		КонецЕсли;

		ИдУзла = УзелСвойство(структЗадача.Запрос, "nodeid");
		НайтиИдУзла = Найти(ИдУзла, "_");
		Если НайтиИдУзла Тогда
			НайтиИдВкладки = Число(Лев(ИдУзла, НайтиИдУзла - 1));
			Вкладка = Вкладки.Получить(НайтиИдВкладки);
			ИдУзла = Сред(ИдУзла, НайтиИдУзла + 1);
		Иначе
			//Возврат Ложь;
		КонецЕсли;

		Если Вкладка = Неопределено Тогда
			Если НЕ ТекущаяВкладка = Неопределено Тогда
				Вкладка = Вкладки.Получить(ТекущаяВкладка);
			Иначе
				Возврат Ложь;
			КонецЕсли;
		КонецЕсли;

		Данные = Вкладка.Данные;
		Если Данные = Неопределено Тогда
			Возврат Ложь;
		КонецЕсли;

		// Если Действие.Имя = Команда["ОбновитьДанные"] Тогда
		// 	ОбщийРезультат = ОбщийРезультат + Вкладка.Данные.ОбновитьДанные(Запрос);
		// 	Возврат Истина;
		// КонецЕсли;

		Узел = Данные.ПолучитьУзел(ИдУзла);

		Если Действие.Имя = Команда["ПриИзменении"] Тогда
			ЗначениеПоля = УзелСвойство(Запрос, "value");
			Если НЕ ЗначениеПоля = Неопределено Тогда
				АтрибутЗначение = Данные.НайтиАтрибут(Узел, "А", "Значение");
				Если НЕ АтрибутЗначение = Неопределено Тогда
					УзелЗначение = Данные.Дочерний(АтрибутЗначение);
					Если НЕ УзелЗначение = Неопределено Тогда
						Данные.УзелУстановитьЗначение(УзелЗначение, ЗначениеПоля);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			Возврат Истина;

		ИначеЕсли Действие.Имя = Команда["ПриНажатии"] Тогда
			АтрибутПриНажатии = Данные.НайтиАтрибут(Узел, "А", "ПриНажатии");
			Если НЕ АтрибутПриНажатии = Неопределено Тогда
				УзелСценарий = Данные.Дочерний(АтрибутПриНажатии);
				Если НЕ УзелСценарий = Неопределено Тогда
					ОбщийРезультат = ОбщийРезультат + Вкладка.Данные.Интерпретировать(УзелСценарий);
				КонецЕсли;
			КонецЕсли;
			Возврат Истина;

		ИначеЕсли Действие.Имя = Команда["ПриОтправке"] Тогда
			АтрибутПриОтправке = Данные.НайтиАтрибут(Узел, "А", "ПриОтправке");
			Если НЕ АтрибутПриОтправке = Неопределено Тогда
				УзелСценарий = Данные.Дочерний(АтрибутПриОтправке);
				Если НЕ УзелСценарий = Неопределено Тогда
					ОбщийРезультат = ОбщийРезультат + Вкладка.Данные.Интерпретировать(УзелСценарий);
				КонецЕсли;
			КонецЕсли;
			Возврат Истина;

		ИначеЕсли Действие.Имя = Команда["МенюРедактора"] Тогда
			ОбщийРезультат = ОбщийРезультат + ПоказатьМенюИнструменты(Вкладка, Узел, УзелСвойство(Запрос, "attr") = "1");
			Возврат Истина;

		ИначеЕсли Действие.Имя = Команда["ОткрытьУзел"] Тогда
			Возврат ОбновитьСостояние(Вкладка, Узел, "УзелОткрыт", Истина);

		ИначеЕсли Действие.Имя = Команда["ЗакрытьУзел"] Тогда
			Возврат ОбновитьСостояние(Вкладка, Узел, "УзелОткрыт", Ложь);

		ИначеЕсли Действие.Имя = Команда["РедактироватьЗначение"] Тогда
			Если УзелСвойство(Запрос, "attr") = "1" Тогда
				ОбновитьСостояние(Вкладка, Узел.Родитель, "ОбновитьУзел", Истина);
			КонецЕсли;
			Возврат ОбновитьСостояние(Вкладка, Узел, "РедактироватьЗначение", Истина);

		ИначеЕсли Действие.Имя = Команда["НовоеЗначениеУзла"] Тогда
			Если УзелСвойство(Запрос, "attr") = "1" Тогда
				ОбновитьСостояние(Вкладка, Узел.Родитель, "ОбновитьУзел", Истина);
			КонецЕсли;
			Если Запрос.Свойство("valuedit") Тогда
				Возврат ОбновитьСостояние(Вкладка, Узел, "НовоеЗначениеУзла", Запрос.valuedit);
			Иначе
				Возврат ОбновитьСостояние(Вкладка, Узел, "РедактироватьЗначение", Ложь);
			КонецЕсли;

		ИначеЕсли Действие.Имя = Команда["РедактироватьИмя"] Тогда

			Если УзелСвойство(Запрос, "attr") = "1" Тогда
				ОбновитьСостояние(Вкладка, Узел.Родитель, "ОбновитьУзел", Истина);
			КонецЕсли;
			Возврат ОбновитьСостояние(Вкладка, Узел, "РедактироватьИмя", Истина);

		ИначеЕсли Действие.Имя = Команда["РедактироватьУзел"] Тогда

			ТекущееОкно = НоваяСтруктураВкладка(Данные, "win", Вкладка.ИмяДанных + "  " + Узел.Имя + ":" + Данные.УзелСвойство(Узел, "Значение"), "struct", Узел.Код, Вкладка.БазаДанных, Вкладка.ИмяДанных, Вкладка.ПозицияДанных);
			ОбновитьСостояние(ТекущееОкно, Узел, "УзелОткрыт", Истина);
			Возврат ОбновитьСостояние(ТекущееОкно, Узел, "РедактироватьУзел", Истина);

		ИначеЕсли Действие.Имя = Команда["ЗагрузитьHTML"] Тогда

			СтруктураHTML = УзелСвойство(Запрос, "enc_value");
			Если СтруктураHTML = Неопределено Тогда
				ТекущееОкно = НоваяСтруктураВкладка(Данные, "win", Вкладка.ИмяДанных + " " + Узел.Имя + ":" + Данные.УзелСвойство(Узел, "Значение") + " import", "struct", Узел.Код, Вкладка.БазаДанных, Вкладка.ИмяДанных, Вкладка.ПозицияДанных);
				ОбновитьСостояние(ТекущееОкно, Узел, "УзелОткрыт", Истина);
				ОбновитьСостояние(ТекущееОкно, Узел, "ЗагрузитьHTML", Истина);
			Иначе
				// Вызвать обработку импорта
				СтруктураHTML = РаскодироватьСтроку(СтруктураHTML, СпособКодированияСтроки.КодировкаURL);
				СтруктураHTML = СтрРазделить(СтруктураHTML, Символы.ПС);
				Данные.ЗагрузитьHTML(Данные.НовыйДочерний(Узел, Новый Структура()), СтруктураHTML, 0);
				ОбновитьСостояние(Вкладка, Узел, "ОбновитьУзел", Истина);
			КонецЕсли;
			Возврат Истина;

		ИначеЕсли Действие.Имя = Команда["НовоеИмяУзла"] Тогда
			Если УзелСвойство(Запрос, "attr") = "1" Тогда
				ОбновитьСостояние(Вкладка, Узел.Родитель, "ОбновитьУзел", Истина);
			КонецЕсли;
			Если Запрос.Свойство("valuedit") Тогда
				Возврат ОбновитьСостояние(Вкладка, Узел, "НовоеИмяУзла", Запрос.valuedit);
			Иначе
				Возврат ОбновитьСостояние(Вкладка, Узел, "РедактироватьИмя", Ложь);
			КонецЕсли;

		ИначеЕсли Действие.Имя = Команда["СохранитьУзел"] Тогда
			Если УзелСвойство(Запрос, "attr") = "1" Тогда
				ОбновитьСостояние(Вкладка, Узел.Родитель, "ОбновитьУзел", Истина);
			КонецЕсли;
			ОбновитьСостояние(Вкладка, Узел, "НовоеИмяУзла", Запрос.name);
			Возврат ОбновитьСостояние(Вкладка, Узел, "НовоеЗначениеУзла", Запрос.value);

		ИначеЕсли Действие.Имя = Команда["НовыйАтрибут"] Тогда
			НовыйУзел = Данные.НовыйАтрибут(Узел, Новый Структура("Имя, Значение", "", ""));
			ОбновитьСостояние(Вкладка, НовыйУзел, "РедактироватьИмя", Истина);
			//ОбновитьСостояние(Вкладка, НовыйУзел, "РедактироватьЗначение", Истина);
			Возврат ОбновитьСостояние(Вкладка, Узел, "ОбновитьУзел", Истина, Истина);

		ИначеЕсли Действие.Имя = Команда["НовыйДочерний"] Тогда
			НовыйУзел = Данные.НовыйДочерний(Узел, Новый Структура("Имя, Значение", "", ""));
			ОбновитьСостояние(Вкладка, НовыйУзел, "РедактироватьИмя", Истина);
			//ОбновитьСостояние(Вкладка, НовыйУзел, "РедактироватьЗначение", Истина);
			ОбновитьСостояние(Вкладка, Узел, "УзелОткрыт", Истина);
			Возврат Истина;

		ИначеЕсли Действие.Имя = Команда["НовыйСоседний"] Тогда
			НовыйУзел = Данные.НовыйСоседний(Узел, Новый Структура("Имя, Значение", "", ""));
			ОбновитьСостояние(Вкладка, НовыйУзел, "РедактироватьИмя", Истина);
			//ОбновитьСостояние(Вкладка, НовыйУзел, "РедактироватьЗначение", Истина);
			ОбновитьСостояние(Вкладка, НовыйУзел.Родитель, "ОбновитьУзел", Истина, Истина);
			Возврат Истина;

		ИначеЕсли Действие.Имя = Команда["НовыйРодитель"] Тогда
			НовыйУзел = Данные.НовыйРодитель(Узел, Новый Структура("Имя, Значение", "", ""));
			ОбновитьСостояние(Вкладка, НовыйУзел, "РедактироватьИмя", Истина);
			ОбновитьСостояние(Вкладка, НовыйУзел, "УзелОткрыт", Истина);
			ОбновитьСостояние(Вкладка, НовыйУзел.Родитель, "ОбновитьУзел", Истина, Истина);
			Возврат Истина;

		ИначеЕсли Действие.Имя = Команда["УдалитьУзел"] Тогда
			ДанныеРодительУзел = Узел.Родитель;
			Данные.УдалитьУзел(Узел);
			ОбновитьСостояние(Вкладка, ДанныеРодительУзел, "ОбновитьУзел", Истина, Истина);
			Возврат Истина;

		ИначеЕсли Действие.Имя = Команда["КопироватьУзел"] Тогда
			Если НЕ Буфер = Неопределено Тогда
				ОсвободитьОбъект(Буфер);
			КонецЕсли;
			Буфер = Новый Соответствие;
			БуферУзел = Узел;
			БуферДанные = Данные;
			Данные.КопироватьУзел(Узел, Буфер);
			Возврат Истина;

		ИначеЕсли Действие.Имя = Команда["ВырезатьУзел"] Тогда
			Если НЕ Буфер = Неопределено Тогда
				ОсвободитьОбъект(Буфер);
			КонецЕсли;
			Буфер = Новый Соответствие;
			БуферУзел = Узел;
			БуферДанные = Данные;
			Данные.КопироватьУзел(Узел, Буфер);
			ДанныеРодительУзел = Узел.Родитель;
			Данные.УдалитьУзел(Узел, Ложь);
			ОбновитьСостояние(Вкладка, ДанныеРодительУзел, "ОбновитьУзел", Истина, Истина);
			Возврат Истина;

		ИначеЕсли Действие.Имя = Команда["ВставитьАтрибут"] Тогда
			Если НЕ Буфер = Неопределено Тогда
				НовыйУзел = БуферДанные.КопироватьВетку(БуферУзел, Данные, Узел, Узел, Истина, , Ложь);
				НовыйУзел.Вставить("Атрибут", Неопределено);
				//НовыйУзел.Вставить("Дочерний", Неопределено);
				УзелАтрибут = Данные.Атрибут(Узел);
				Узел.Вставить("Атрибут", НовыйУзел.Код);
				Если НЕ УзелАтрибут = Неопределено Тогда
					НовыйУзел.Вставить("Соседний", УзелАтрибут.Код);
					УзелАтрибут.Вставить("Старший", НовыйУзел);
				КонецЕсли;
			КонецЕсли;
			Возврат ОбновитьСостояние(Вкладка, Узел, "ОбновитьУзел", Истина, Истина);

		ИначеЕсли Действие.Имя = Команда["ВставитьДочерний"] Тогда
			Если НЕ Буфер = Неопределено Тогда
				НовыйУзел = БуферДанные.КопироватьВетку(БуферУзел, Данные, Узел, Узел, , , Ложь);
				УзелДочерний = Данные.Дочерний(Узел);
				Узел.Вставить("Дочерний", НовыйУзел.Код);
				Если НЕ УзелДочерний = Неопределено Тогда
					НовыйУзел.Вставить("Соседний", УзелДочерний.Код);
					УзелДочерний.Вставить("Старший", НовыйУзел);
				КонецЕсли;
			КонецЕсли;
			Возврат ОбновитьСостояние(Вкладка, Узел, "УзелОткрыт", Истина, Истина);

		ИначеЕсли Действие.Имя = Команда["ВставитьСоседний"] Тогда
			Если НЕ Буфер = Неопределено Тогда
				НовыйУзел = БуферДанные.КопироватьВетку(БуферУзел, Данные, Узел, Узел.Родитель, , , Ложь);
				УзелСоседний = Данные.Соседний(Узел);
				Узел.Вставить("Соседний", НовыйУзел.Код);
				Если НЕ УзелСоседний = Неопределено Тогда
					НовыйУзел.Вставить("Соседний", УзелСоседний.Код);
					УзелСоседний.Вставить("Старший", НовыйУзел);
				КонецЕсли;
			КонецЕсли;
			Возврат ОбновитьСостояние(Вкладка, НовыйУзел.Родитель, "ОбновитьУзел", Истина, Истина);

		ИначеЕсли Действие.Имя = Команда["УдалитьАтрибуты"] Тогда
			Если НЕ УзелСвойство(Узел, "Атрибут") = Неопределено Тогда
				Данные.УдалитьУзел(Данные.Атрибут(Узел), Ложь);
				//Узел.Удалить("Атрибут");
				Узел.Атрибут = Неопределено;
			КонецЕсли;
			Возврат ОбновитьСостояние(Вкладка, Узел, "ОбновитьУзел", Истина, Истина);

		ИначеЕсли Действие.Имя = Команда["УдалитьРодителя"] Тогда
			Если НЕ УзелСвойство(Узел, "Родитель") = Неопределено Тогда
				Данные.УдалитьРодителя(Узел);
			КонецЕсли;
			ОбновитьСостояние(Вкладка, Узел.Родитель, "ОбновитьУзел", Истина, Истина);
			Возврат ОбновитьСостояние(Вкладка, Узел, "ОбновитьУзел", Истина, Истина);

		// ИначеЕсли Действие.Имя = Команда["УзелСтруктура"] Тогда
		// 	Возврат ОбновитьСостояние(Вкладка, Узел, "Изменить", Истина, , Истина);
		//
		// ИначеЕсли Действие.Имя = Команда["УзелПросмотр"] Тогда
		// 	Возврат ОбновитьСостояние(Вкладка, Узел, "Изменить", Ложь, , Истина);

		ИначеЕсли Действие.Имя = Команда["СтруктураДанных"] Тогда

			ТекущееОкно = НоваяСтруктураВкладка(Данные, "win", Вкладка.ИмяДанных + " " + Узел.Имя + ":" + Данные.УзелСвойство(Узел, "Значение"), "struct", Вкладка.ИдУзла, Вкладка.БазаДанных, Вкладка.ИмяДанных, Вкладка.ПозицияДанных);
			ОбновитьСостояние(ТекущееОкно, Узел, "УзелОткрыт", Истина);
			Возврат Истина;

		ИначеЕсли Действие.Имя = Команда["ЗначениеУзла"] Тогда

			//Данные.Обновить = Истина;
			УзелЗначение = Данные.Интерпретировать(Узел);
			//Данные.Обновить = Ложь;
			Если ТипЗнч(УзелЗначение) = Тип("Структура") Тогда
				ТипУзла = "struct";
				ДанныеВкладки = Данные;
				УзелКод = УзелЗначение.Код;
			Иначе
				ТипУзла = "value";
				ДанныеВкладки = Новый Структура("Представление, Фронт", УзелЗначение, Узел);
				УзелКод = Узел.Код;
			КонецЕсли;
			ТекущееОкно = НоваяСтруктураВкладка(ДанныеВкладки, "win", Вкладка.ИмяДанных + " Значение " + " " + Узел.Имя + ":" + Данные.УзелСвойство(Узел, "Значение"), ТипУзла, УзелКод, "", "", "");
			Возврат Истина;

		// ИначеЕсли Действие.Имя = Команда["ЗначениеОбъекта"] Тогда
		//
		// 	Вкладка.Данные.ПолучитьОпределениеОбъекта(Узел);
		// 	ОбновитьСостояние(ТекущееОкно, Узел, "УзелОткрыт", Истина);
		// 	ОбновитьСостояние(Вкладка, Узел, "ОбновитьУзел", Истина);
		// 	Возврат Истина;

		КонецЕсли;

	КонецЕсли;

	Возврат Ложь;
КонецФункции // ВыполнитьДействия()


Функция ПередатьСтроку(Соединение, СтрокаДанные) Экспорт
	Попытка
		Соединение = Новый TCPСоединение(Хост, Порт);
		Соединение.ТаймаутОтправки = 50;
		Соединение.ОтправитьДвоичныеДанные(ПолучитьДвоичныеДанныеИзСтроки(СтрокаДанные));
		КоличествоПопыток = 50;
		Возврат Истина;
	Исключение
		Соединение = Неопределено;
		КоличествоПопыток = КоличествоПопыток - 1;
		//Сообщить("Осталось попыток: " + КоличествоПопыток);
		Если КоличествоПопыток = 0 Тогда
			Сообщить("Хост недоступен");
		КонецЕсли;
		Возврат Ложь;
	КонецПопытки;
КонецФункции // ПередатьСтроку()


Функция ЗаписатьСобытие(стрЗаголовок, стрСообщение, ТипСобытия = 0, ПараметрКоманда = "") Экспорт
	Если НЕ Журнал = Неопределено Тогда
		НоваяСтрока = Журнал.Объект.НовыйДочерний(Журнал.СписокСтрок, Новый Структура("Имя", "Строка"), Истина);
		НовыйАтрибут = Журнал.Объект.НовыйАтрибут(НоваяСтрока, Новый Структура("Имя, Значение", "Время", ТекущаяДата()), Истина);
		НовыйАтрибут = Журнал.Объект.НовыйСоседний(НовыйАтрибут, Новый Структура("Имя, Значение", "Сообщение", ?(ТипСобытия = 0, "Общее", ?(ТипСобытия = 1, "Успех", ?(ТипСобытия = 2, "Внимание", "Ошибка"))) + " [" + стрЗаголовок + "] " + стрСообщение), Истина);
		Журнал.Объект.ОбновитьУзел(Журнал.Данные);
	КонецЕсли;
	ОбщийРезультат = ОбщийРезультат + Шаблон.ПолучитьОбласть("ОбластьУведомление",
		Новый Структура("ПараметрТекстУведомления, ПараметрЗаголовокУведомления, ПараметрТипУведомления, ПараметрКоманда",
			стрСообщение, стрЗаголовок, ТипСобытия,	ПараметрКоманда));
КонецФункции // ЗаписатьСобытие()


Если АргументыКоманднойСтроки.Количество() Тогда

	procid = АргументыКоманднойСтроки[0];
	Порт = АргументыКоманднойСтроки[1];

	Гость = (procid = "0");
	Субъект = "Гость";

	Шаблон = ЗагрузитьСценарий(ОбъединитьПути(ТекущийКаталог(), "template.os"));
	Шаблон.ЗагрузитьМакет(ОбъединитьПути(ТекущийКаталог(), "resource", "showdata.html"));

	ПодключитьСценарий(ОбъединитьПути(ТекущийКаталог(), "pagedata.os"), "pagedata");

	ОбщийРезультат = "";
	НомерОбновления = 0;

	ВсеДанные = Новый Соответствие;

	Если НЕ Гость Тогда
		Попытка
			Журнал = Новый Структура("Объект, Данные, СписокСтрок");
			Журнал.Объект = Новый pagedata(ЭтотОбъект, "", "log", "");
			ВсеДанные.Вставить("/log/", Журнал.Объект);
			// Узел куда вставлять события
			Журнал.Данные = Журнал.Объект.НайтиУзел(Журнал.Объект.Корень, "Данные");
			Журнал.СписокСтрок = Журнал.Объект.Дочерний(Журнал.Данные); // Ссылка
			ЗаписатьСобытие("Процесс", "Запущен процесс procid=" + procid);
		Исключение
			Сообщить(ОписаниеОшибки());
			Журнал = Неопределено;
		КонецПопытки;

		Попытка
			Настройки = Новый pagedata(ЭтотОбъект, "", "cfg", "");
			ВсеДанные.Вставить("/cfg/", Настройки);
		Исключение
			ЗаписатьСобытие("Процесс", ОписаниеОшибки());
			Настройки = Неопределено;
		КонецПопытки;
	КонецЕсли;

	ВкладкиСписок = Новый СписокЗначений;
	Вкладки = Новый Соответствие;
	ИдВкладки = 0;

	Команда = Новый Соответствие;
	Команда.Вставить("ОткрытьУзел", "nodeopen");
	Команда.Вставить("ЗакрытьУзел", "nodeclose");
	// Команда.Вставить("УзелСтруктура", "struct");
	// Команда.Вставить("УзелПросмотр", "view");
	Команда.Вставить("ОбновитьУзел", "nodereload");
	Команда.Вставить("НоваяВкладка", "newtab");
	Команда.Вставить("ВыбратьВкладку", "tabselect");
	Команда.Вставить("ЗакрытьВкладку", "tabclose");
	Команда.Вставить("РедактироватьЗначение", "valuedit");
	Команда.Вставить("ЗначениеУзла", "showvalue");
	// Команда.Вставить("ЗначениеОбъекта", "showenv");
	Команда.Вставить("НовоеЗначениеУзла", "submitvalue");
	Команда.Вставить("НовоеИмяУзла", "submitname");
	Команда.Вставить("РедактироватьИмя", "namedit");
	Команда.Вставить("РедактироватьУзел", "editnode");
	Команда.Вставить("ЗагрузитьHTML", "importhtml");
	Команда.Вставить("СохранитьУзел", "savenode");
	Команда.Вставить("НовыйРодитель", "paradd");
	Команда.Вставить("НовыйАтрибут", "attradd");
	Команда.Вставить("НовыйДочерний", "childadd");
	Команда.Вставить("СтруктураДанных", "structwin");
	Команда.Вставить("СохранитьДанные", "savedata");
	Команда.Вставить("РежимРедактор", "designmode");
	Команда.Вставить("РежимПросмотр", "viewmode");
	// Команда.Вставить("ОбновитьДанные", "upddata");
	Команда.Вставить("ПриИзменении", "onchange");
	Команда.Вставить("ПриНажатии", "onclick");
	Команда.Вставить("ПриОтправке", "onsubmit");
	Команда.Вставить("МенюРедактора", "designmenu");
	Команда.Вставить("НовыйСоседний", "nextadd");
	Команда.Вставить("УдалитьУзел", "noderemove");
	Команда.Вставить("КопироватьУзел", "nodecopy");
	Команда.Вставить("ВырезатьУзел", "nodecut");
	Команда.Вставить("ВставитьАтрибут", "nodepasteattr");
	Команда.Вставить("ВставитьДочерний", "nodepastechild");
	Команда.Вставить("ВставитьСоседний", "nodepastenext");
	Команда.Вставить("УдалитьАтрибуты", "attremove");
	Команда.Вставить("УдалитьРодителя", "parremove");
	Команда.Вставить("ЗавершитьПроцесс", "killproc");

	Буфер = Неопределено;

	Хост = "127.0.0.1";
	//Порт = 8888;
	ПараметрХост = ?(НЕ Порт = "8888", "http://vasvl123.github.io/OneScriptDB" , ""); // чтобы быстрее грузилось

	Соединение = Неопределено;

	Задачи = Новый СписокЗначений;

	СтруктЗапрос = СтруктуруВСтроку(Новый Структура("procid", procid));
	КоличествоПопыток = 100;

	ЗавершитьПроцесс = Ложь;

	Попытка

		Пока Истина Цикл

			ПрерватьЦикл = Ложь;
			Пока Не ПрерватьЦикл Цикл
				ПрерватьЦикл = Истина;
				Для каждого элЗадача Из Задачи Цикл
					структЗадача = ЭлЗадача.Значение;
					Если структЗадача.Тип = "Запрос" Тогда
						Попытка
							ЗапросВыполнитьЗадачу(структЗадача);
						Исключение
							Сообщить(ОписаниеОшибки());
							структЗадача.Этап = "УдалитьЗадачу";
						КонецПопытки;
					КонецЕсли;
					Если структЗадача.Этап = "УдалитьЗадачу" Тогда
						Задачи.Удалить(элЗадача);
						ПрерватьЦикл = Ложь;
						Прервать;
					КонецЕсли;
				КонецЦикла;
			КонецЦикла;

			Если ЗавершитьПроцесс Тогда
				Прервать;
			КонецЕсли;

			Если Соединение = Неопределено Тогда
				Если НЕ ПередатьСтроку(Соединение, СтруктЗапрос) Тогда
					Если НЕ КоличествоПопыток > 0 Тогда
						ЗавершитьПроцесс = Истина;
						Продолжить;
					КонецЕсли;
				КонецЕсли;
			Иначе
				Если НЕ Соединение.Активно Тогда
					Соединение = Неопределено;
					Продолжить;
				КонецЕсли;
			КонецЕсли;

			Попытка
				ЗадачиКоличество = Задачи.Количество();
				Если ЗадачиКоличество > 1 Тогда
					Соединение.ТаймаутЧтения = 50;
				КонецЕсли;
				Запрос = Соединение.ПрочитатьСтроку();
			Исключение
				//Сообщить("Осталось задач: " + ЗадачиКоличество);
				Запрос = "";
			КонецПопытки;

			Если Запрос = "" Тогда
				Продолжить;
			КонецЕсли;

			Попытка
				Запрос = СтрокуВСтруктуру(Запрос);
				domupdate = "" + УзелСвойство(Запрос, "domupdate");
				структЗадача = Новый Структура("Тип, Этап, Запрос, Действие, Результат", "Запрос", "ВыполнитьЗадачу", Запрос, Неопределено, "");
			Исключение
				структЗадача = Новый Структура("Тип, Этап, Запрос, Результат", "Запрос", "ЕстьРезультат", Новый Структура("taskid", ""), "Неверный запрос");
			КонецПопытки;

			Задачи.Добавить(структЗадача);

			Соединение.Закрыть();
			Соединение = Неопределено;

		КонецЦикла;

	Исключение
		Сообщить(ОписаниеОшибки());
	КонецПопытки;

КонецЕсли;

Сообщить("Процесс procid=" + procid + " завершен.");
