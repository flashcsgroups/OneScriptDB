Перем ВсеДанные, procid, data;
Перем Шаблон;

Функция СкопироватьДанные(Знач Данные)

	Возврат Данные;

КонецФункции


Функция НачальнаяСтраница()
	Перем Ответ;

	Ответ = Шаблон.ПолучитьОбласть("ОбластьШапка", Новый Структура("ПараметрИдПроцесса", procid));
	Ответ = Ответ + Шаблон.ПолучитьОбласть("ОбластьТело", Новый Структура("ПараметрИдПроцесса, ПараметрИдУзла", procid, "316"));

	//Ответ = Ответ + Шаблон.ПолучитьОбласть("ОбластьПриветствие");

	Ответ = Ответ + Шаблон.ПолучитьОбласть("ОбластьПодвал");

	Возврат Ответ;

КонецФункции


Функция СтрокуВСтруктуру(Стр)
	Стр = СтрРазделить(Стр, Символы.Таб);
	Ключ = Неопределено;
	Рез = Неопределено;
	Для Каждого знСтр Из Стр Цикл
		Если Ключ = Неопределено Тогда
			Ключ = знСтр;
		Иначе
			Если Рез = Неопределено Тогда
				Рез = Новый Структура;
			КонецЕсли;
			Рез.Вставить(Ключ, знСтр);
			Ключ = Неопределено;
		КонецЕсли;
	КонецЦикла;
	Возврат Рез;
КонецФункции


Функция СтруктуруВСтроку(Структ)
	Результат = "";
	Для каждого Элемент Из Структ Цикл
		Результат = Результат + ?(Результат = "", "", Символы.Таб) + Элемент.Ключ + Символы.Таб + Элемент.Значение;
	КонецЦикла;
	Возврат Результат;
КонецФункции


Функция ПоказатьУзел(Данные, Запрос, КодУзла = Неопределено, Уровень = "", ПервыйВызов = Истина) Экспорт

	Если КодУзла = Неопределено Тогда
		КодУзла = Запрос.nodeid;
	КонецЕсли;

	Узел = Данные.ПолучитьУзел(КодУзла);
	Если Узел = Неопределено Тогда
		Возврат "Узел не найден!";
	КонецЕсли;

	Текст = Символы.ПС + Уровень;

	Если Узел.Имя = "#text" Тогда
		Если Узел.Свойство("Значение") Тогда
			Текст = Текст + Узел.Значение;
		Иначе
			Текст = "";
		КонецЕсли;
	ИначеЕсли Узел.Имя = "#comment" Тогда
		Если Узел.Свойство("Значение") Тогда
			Текст = Текст + "<!-- " + Узел.Значение + " -->";
		КонецЕсли;
	Иначе
		Текст = Текст + "<" + Узел.Имя;

		Атрибут = Неопределено;
		Если Узел.Свойство("Атрибут", Атрибут) Тогда
			Атрибут = Данные.ПолучитьУзел(Атрибут);
			Пока НЕ Атрибут = Неопределено Цикл
				АтрибутИмя = Атрибут.Имя;
				АтрибутИмя = СтрЗаменить(АтрибутИмя, "xml_lang", "xml:lang");
				АтрибутИмя = СтрЗаменить(АтрибутИмя, "_", "-");
				АтрибутЗначение = Атрибут.Значение;
				Текст = Текст + " " + АтрибутИмя + "=""" + АтрибутЗначение + """";
				Если Атрибут.Свойство("Соседний", Атрибут) Тогда
					Атрибут = Данные.ПолучитьУзел(Атрибут)
				КонецЕсли;
		    КонецЦикла;
		КонецЕсли;

		Текст = Текст + ">";
	КонецЕсли;

	Если Узел.Свойство("Дочерний") Тогда
		Текст = Текст + ПоказатьУзел(Данные, Запрос, Узел.Дочерний, Уровень + Символы.Таб, Ложь);
	КонецЕсли;

	Если НЕ (Узел.Имя = "#text" ИЛИ Узел.Имя = "#comment") Тогда
		Текст = Текст + Символы.ПС + Уровень + "</" + Узел.Имя + ">";
	КонецЕсли;

	Если НЕ ПервыйВызов Тогда
		Если Узел.Свойство("Соседний") Тогда
			Текст = Текст + ПоказатьУзел(Данные, Запрос, Узел.Соседний, Уровень, Ложь);
		КонецЕсли;
	КонецЕсли;

	Возврат Текст;

КонецФункции // ПоказатьУзел()


Функция ПоказатьСтруктуруУзла(Данные, Запрос, КодУзла = Неопределено, этоАтрибут = Ложь, ПервыйВызов = Истина) Экспорт

	Представление = "";
	Атрибуты = "";

	Если КодУзла = Неопределено Тогда
		КодУзла = Запрос.nodeid;
	КонецЕсли;

	Узел = Данные.ПолучитьУзел(КодУзла);
	Если Узел = Неопределено Тогда
		Возврат "Узел не найден!";
	КонецЕсли;

	Если НЕ Узел.Свойство("nodeopen") Тогда
		Узел.Вставить("nodeopen", Ложь);
	КонецЕсли;

	УзелИмя = Узел.Имя;

	УзелЗначение = "";
	Если Узел.Свойство("Значение") Тогда
		УзелЗначение = Узел.Значение;
	КонецЕсли;

	Если этоАтрибут Тогда
		УзелИмя = СтрЗаменить(УзелИмя, "xml_lang", "xml:lang");
		УзелИмя = СтрЗаменить(УзелИмя, "_", "-");
	КонецЕсли;

	ГлУзелИзменить = Неопределено;
	УзелИзменить = Неопределено;
	УзелРежим = Неопределено;
	Узел.Свойство("mainodedit", ГлУзелИзменить);
	Узел.Свойство("nodedit", УзелИзменить);
	Узел.Свойство("mode", УзелРежим);

	КнопкаУзел = "";

	Если Узел.Свойство("Дочерний") Тогда
		КнопкаУзел = Шаблон.ПолучитьОбласть("ОбластьКнопкаУзел",
			Новый Структура("ПараметрИдУзла, ПараметрИдПроцесса, ПараметрРежим, ПараметрКоманда, ПараметрНадписьНаКнопке",
				Узел.Код,
				Запрос.procid,
				Запрос.mode,
				?(Узел.nodeopen, "nodeclose", "nodeopen"),
				?(Узел.nodeopen, "○" , "◉")));
	КонецЕсли;

	КнопкиИнструменты = "";

	Если УзелИзменить = Истина Тогда
		КнопкиИнструменты =
			Шаблон.ПолучитьОбласть("ОбластьКнопкаИнструменты",
				Новый Структура("ПараметрИдУзла, ПараметрНеактивно, ПараметрИдПроцесса, ПараметрРежим, ПараметрКоманда, ПараметрВидимость, ПараметрНадписьНаКнопке",
					Узел.Код,
					"",
					Запрос.procid,
					Запрос.mode,
					?(УзелРежим = "view", "modesource", "modeview"),
					"",
					?(УзелРежим = "view", "Стр", "Пр"))) +
			Шаблон.ПолучитьОбласть("ОбластьКнопкаИнструменты",
				Новый Структура("ПараметрИдУзла, ПараметрНеактивно, ПараметрИдПроцесса, ПараметрРежим, ПараметрКоманда, ПараметрВидимость, ПараметрНадписьНаКнопке",
					Узел.Код,
					"",
					Запрос.procid,
					Запрос.mode,
					"attradd",
					"",
					"+Атр")) +
			Шаблон.ПолучитьОбласть("ОбластьКнопкаИнструменты",
				Новый Структура("ПараметрИдУзла, ПараметрНеактивно, ПараметрИдПроцесса, ПараметрРежим, ПараметрКоманда, ПараметрВидимость, ПараметрНадписьНаКнопке",
					Узел.Код,
					"",
					Запрос.procid,
					Запрос.mode,
					"nodeadd",
					"",
					"+Доч"));

	КонецЕсли;

	ПараметрИмяУзла = КнопкаУзел +
		Шаблон.ПолучитьОбласть("ОбластьКнопкаИмяЗначение",
			Новый Структура("ПараметрИдУзла, ПараметрНеактивно1, ПараметрНеактивно2, ПараметрИдПроцесса, ПараметрРежим, ПараметрИнструменты, ПараметрКоманда1, ПараметрКоманда2, ПараметрИмя, ПараметрВидимость, ПараметрЗначение",
				Узел.Код,
				?(этоАтрибут, ?(ГлУзелИзменить = Истина, "", "disabled"), ""),
				?(УзелИзменить = Истина, "", "disabled"),
				Запрос.procid,
				Запрос.mode,
				КнопкиИнструменты,
				?(УзелИзменить = Истина, "namedit", "nodedit"),
				"valuedit",
				УзелИмя,
				?(УзелЗначение = "", "style=""visibility: hidden""", ""),
				УзелЗначение));

	Если Узел.Свойство("Атрибут") Тогда
		УзелАтрибут = Данные.Атрибут(Узел);
		УзелАтрибут.Вставить("mainodedit", УзелИзменить = Истина);
		Атрибуты = ПоказатьСтруктуруУзла(Данные, Запрос, Узел.Атрибут, Истина, Ложь);
	КонецЕсли;

	Если этоАтрибут Тогда
		Если Узел.Свойство("Соседний") Тогда
			УзелАтрибут = Данные.Соседний(Узел);
			УзелАтрибут.Вставить("mainodedit", ГлУзелИзменить = Истина);
			ПараметрИмяУзла = ПараметрИмяУзла + ПоказатьСтруктуруУзла(Данные, Запрос, Узел.Соседний, Истина, Ложь);
		КонецЕсли;

		Возврат ПараметрИмяУзла;
	КонецЕсли;

	ПараметрЗаголовокУзла = ПараметрИмяУзла + Атрибуты;

	ДочернийУзел = "";
	Если Узел.nodeopen Тогда
		Если УзелРежим = "view" Тогда
			ДочернийУзел = ПоказатьУзел(Данные, Запрос, Узел.Код);
		Иначе
			Если Узел.Свойство("Дочерний") Тогда
				ДочернийУзел = ПоказатьСтруктуруУзла(Данные, Запрос, Узел.Дочерний, , Ложь);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

	Представление = Шаблон.ПолучитьОбласть("ОбластьУзел",
		Новый Структура("ПараметрИдУзла, ПараметрЗаголовокУзла, ПараметрДочернийУзел",
			Узел.Код,
			ПараметрЗаголовокУзла,
			ДочернийУзел));

	Если НЕ ПервыйВызов Тогда
		Если Узел.Свойство("Соседний") Тогда
				Представление = Представление + ПоказатьСтруктуруУзла(Данные, Запрос, Узел.Соседний, , Ложь);
		КонецЕсли;
	КонецЕсли;

	Возврат Представление;

КонецФункции

Функция ВыполнитьЗадачу(структЗадача)

	Запрос = структЗадача.Запрос;

	Ответ = Неопределено;

	Если Запрос.Свойство("data") Тогда
		data = Запрос.data;
	КонецЕсли;

	Если data = Неопределено Тогда
		Ответ = НачальнаяСтраница();
	Иначе
		Данные = ВсеДанные.Получить(data);
		Если Данные = Неопределено Тогда
			Данные = Новый pagedata("" + data + ".txt", Шаблон);
			ВсеДанные.Вставить("" + data, Данные);
		КонецЕсли;
		Если Запрос.Свойство("cmd") Тогда
			ОбработатьДанные(Данные, Запрос);
		КонецЕсли;
		Если Запрос.Свойство("mode") Тогда
			Если Запрос.mode = "design" Тогда
				Ответ = ПоказатьСтруктуруУзла(Данные, Запрос);
			КонецЕсли;
			Если Запрос.mode = "lisp" Тогда
				Ответ = ПоказатьСтруктуруУзла(Данные, Запрос);
			КонецЕсли;
		КонецЕсли;
		Если Ответ = Неопределено Тогда
			Ответ = ПоказатьУзел(Данные, Запрос);
		КонецЕсли;
	КонецЕсли;

	структЗадача.Результат = Ответ;

КонецФункции // ВыполнитьЗадачу()


Функция ОбработатьДанные(Данные, Запрос, КодУзла = Неопределено) Экспорт

	ПервыйВызов = (КодУзла = Неопределено);

	Если ПервыйВызов Тогда
		КодУзла = Запрос.nodeid;
	КонецЕсли;

	Узел = Данные.ПолучитьУзел(КодУзла);
	Если Узел = Неопределено Тогда
		Возврат "Узел не найден!";
	КонецЕсли;

	Если Запрос.cmd = "nodeopen" Тогда
		Узел.Вставить("nodeopen", Истина);
		Если Запрос.mode = "lisp" Тогда
			Попытка
				Значение = Данные.Интерпретировать(Данные.Окружение, Узел);
			Исключение
				Значение = ОписаниеОшибки();
			КонецПопытки;
			Узел.Вставить("Значение", Значение);
		КонецЕсли;
	ИначеЕсли Запрос.cmd = "nodeclose" Тогда
		Узел.Вставить("nodeopen", Ложь);
		Узел.Вставить("nodedit", Ложь);
	ИначеЕсли Запрос.cmd = "nodedit" Тогда
		Узел.Вставить("nodedit", Истина);
	ИначеЕсли Запрос.cmd = "modeview" Тогда
		Узел.Вставить("mode", "view");
		Узел.Вставить("nodeopen", Истина);
	ИначеЕсли Запрос.cmd = "modesource" Тогда
		Узел.Вставить("mode", "source");
		Узел.Вставить("nodeopen", Истина);
	КонецЕсли;

КонецФункции // ОбработатьДанные()


Если АргументыКоманднойСтроки.Количество() Тогда
	procid = АргументыКоманднойСтроки[0];
Иначе
	procid = "1";
КонецЕсли;

Шаблон = ЗагрузитьСценарий(ОбъединитьПути(ТекущийКаталог(), "template.os"));
Шаблон.ЗагрузитьМакет("showdata");

//ПодключитьСценарий(ОбъединитьПути(ТекущийКаталог(), "dbaccess.os"), "dbaccess");
ПодключитьСценарий(ОбъединитьПути(ТекущийКаталог(), "pagedata.os"), "pagedata");

ВсеДанные = Новый Соответствие;

Таймаут = 10;
Хост = "127.0.0.1";
Порт = 8888;
Соединение = Неопределено;

Задачи = Новый Соответствие;

КоличествоПопыток = 100;

Попытка

	Пока Истина Цикл

		КоличествоПопыток = КоличествоПопыток - 1;
		Если КоличествоПопыток = 0 Тогда
			Сообщить("Хост недоступен");
			Прервать;
		КонецЕсли;

		Для каждого элЗадача Из Задачи Цикл
			структЗадача = ЭлЗадача.Значение;
			Если структЗадача.Результат = Неопределено Тогда
				ВыполнитьЗадачу(структЗадача);
			КонецЕсли;
			Если НЕ структЗадача.Результат = Неопределено Тогда
				структЗадача.Соединение.ОтправитьСтроку(структЗадача.Результат);
				структЗадача.Соединение.Закрыть();
				Задачи.Удалить(элЗадача.Ключ);
				Прервать;
			КонецЕсли;
		КонецЦикла;

		Если Соединение = Неопределено Тогда
			Попытка
				Соединение = Новый TCPСоединение(Хост, Порт);
				Соединение.ТаймаутОтправки = 100;
				СтруктЗапрос = Новый Структура("procid", procid);
				Соединение.ОтправитьСтроку(СтруктуруВСтроку(СтруктЗапрос));
				КоличествоПопыток = 100;
			Исключение
				Продолжить;
			КонецПопытки;
		Иначе
			Если НЕ Соединение.Активно Тогда
				Соединение = Неопределено;
				Продолжить;
			КонецЕсли;
		КонецЕсли;

		Попытка
			ЗадачиКоличество = Задачи.Количество();
			Если ЗадачиКоличество Тогда
				Соединение.ТаймаутЧтения = 100;
			КонецЕсли;
			Запрос = Соединение.ПрочитатьСтроку();
		Исключение
			Сообщить("Осталось задач: " + ЗадачиКоличество);
			Продолжить;
		КонецПопытки;

		Попытка
			Запрос = СтрокуВСтруктуру(Запрос);
			структЗадача = Новый Структура("Запрос, Соединение, Результат", Запрос, Соединение);
		Исключение
			структЗадача = Новый Структура("Запрос, Соединение, Результат", Неопределено, Соединение, "Неверный запрос");
		КонецПопытки;

		Задачи.Вставить(Запрос.taskid, структЗадача);

		Соединение = Неопределено;

	КонецЦикла;

Исключение

	Сообщить(ОписаниеОшибки());

КонецПопытки;
