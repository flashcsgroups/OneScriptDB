Перем Вкладки, procid, data;
Перем Шаблон;
Перем Буфер;
Перем БуферУзел;
Перем События, ИдСобытия;
Перем Хост, Порт;


Функция ПоказатьВкладки()
	Текст = "";
	ПараметрыШаблона = Новый Структура;
	ПараметрыШаблона.Вставить("ПараметрИдПроцесса", procid);
	Для каждого элВкладка Из Вкладки Цикл
		Вкладка = элВкладка.Значение;
		ПараметрыШаблона.Вставить("ПараметрРежим", Вкладка.Режим);
		ПараметрыШаблона.Вставить("ПараметрДанные", элВкладка.Ключ);
		ПараметрыШаблона.Вставить("ПараметрИдУзла", элВкладка.ИдУзла);
		ПараметрыШаблона.Вставить("ПараметрКоманда", "nodeopen");
		ПараметрыШаблона.Вставить("ПараметрЗаголовок", элВкладка.Заголовок);
		Текст = Текст + Шаблон.ПолучитьОбласть("ОбластьВкладка", ПараметрыШаблона);
	КонецЦикла;
	ПараметрыШаблона.Вставить("ПараметрРежим", "view");
	ПараметрыШаблона.Вставить("ПараметрДанные", "newpage");
	ПараметрыШаблона.Вставить("ПараметрИдУзла", 1);
	ПараметрыШаблона.Вставить("ПараметрКоманда", "newpage");
	ПараметрыШаблона.Вставить("ПараметрЗаголовок", "✚");
	Текст = Текст + Шаблон.ПолучитьОбласть("ОбластьВкладка", ПараметрыШаблона);
	Возврат Текст;
КонецФункции // ПоказатьВкладки()


Функция НоваяСтраница()
	Перем Ответ;
	Ответ = Шаблон.ПолучитьОбласть("ОбластьНоваяСтраница");
	Возврат Ответ;
КонецФункции


Функция НачальнаяСтраница()
	Перем Ответ;
	Ответ = Шаблон.ПолучитьОбласть("ОбластьШапка", Новый Структура("ПараметрИдПроцесса", procid));
	Ответ = Ответ + Шаблон.ПолучитьОбласть("ОбластьПанель", Новый Структура("ПараметрВкладки", ПоказатьВкладки()));
	Ответ = Ответ + Шаблон.ПолучитьОбласть("ОбластьНоваяСтраница");
	Ответ = Ответ + Шаблон.ПолучитьОбласть("ОбластьПодвал");
	Возврат Ответ;
КонецФункции


Функция СтрокуВСтруктуру(Стр)
	Стр = СтрРазделить(Стр, Символы.Таб);
	Ключ = Неопределено;
	Рез = Неопределено;
	Для Каждого знСтр Из Стр Цикл
		Если Ключ = Неопределено Тогда
			Ключ = знСтр;
		Иначе
			Если Рез = Неопределено Тогда
				Рез = Новый Структура;
			КонецЕсли;
			Рез.Вставить(Ключ, знСтр);
			Ключ = Неопределено;
		КонецЕсли;
	КонецЦикла;
	Возврат Рез;
КонецФункции


Функция СтруктуруВСтроку(Структ)
	Результат = "";
	Для каждого Элемент Из Структ Цикл
		Результат = Результат + ?(Результат = "", "", Символы.Таб) + Элемент.Ключ + Символы.Таб + Элемент.Значение;
	КонецЦикла;
	Возврат Результат;
КонецФункции


Функция ПоказатьУзел(Данные, Запрос, КодУзла = Неопределено, Уровень = "", ПервыйВызов = Истина) Экспорт

	Если КодУзла = Неопределено Тогда
		КодУзла = Запрос.nodeid;
	КонецЕсли;

	Узел = Данные.ПолучитьУзел(КодУзла);
	Если Узел = Неопределено Тогда
		Возврат "Узел " + КодУзла + " не найден!";
	КонецЕсли;

	Текст = Символы.ПС + Уровень;

	Если Узел.Имя = "#text" Тогда
		Если Узел.Свойство("Значение") Тогда
			Текст = Текст + Узел.Значение;
		Иначе
			Текст = "";
		КонецЕсли;
	ИначеЕсли Узел.Имя = "#comment" Тогда
		Если Узел.Свойство("Значение") Тогда
			Текст = Текст + "<!-- " + Узел.Значение + " -->";
		КонецЕсли;
	Иначе
		Текст = Текст + "<" + Узел.Имя;

		Атрибут = Неопределено;
		Если НЕ УзелСвойство(Узел, "Атрибут") = Неопределено Тогда
			Атрибут = Данные.ПолучитьУзел(Узел.Атрибут);
			Пока НЕ Атрибут = Неопределено Цикл
				АтрибутИмя = Атрибут.Имя;
				АтрибутИмя = СтрЗаменить(АтрибутИмя, "xml_lang", "xml:lang");
				АтрибутИмя = СтрЗаменить(АтрибутИмя, "_", "-");
				АтрибутЗначение = Атрибут.Значение;
				Текст = Текст + " " + АтрибутИмя + "=""" + АтрибутЗначение + """";
				Если Атрибут.Свойство("Соседний", Атрибут) Тогда
					Атрибут = Данные.ПолучитьУзел(Атрибут)
				КонецЕсли;
		    КонецЦикла;
		КонецЕсли;

		Текст = Текст + ">";
	КонецЕсли;

	Если НЕ УзелСвойство(Узел, "Дочерний") = Неопределено Тогда
		Текст = Текст + ПоказатьУзел(Данные, Запрос, Узел.Дочерний, Уровень + Символы.Таб, Ложь);
	КонецЕсли;

	Если НЕ (Узел.Имя = "#text" ИЛИ Узел.Имя = "#comment") Тогда
		Если Узел.Свойство("Значение") Тогда
			Текст = Текст + Узел.Значение;
		КонецЕсли;
		Текст = Текст + Символы.ПС + Уровень + "</" + Узел.Имя + ">";
	КонецЕсли;

	Если НЕ ПервыйВызов Тогда
		Если НЕ УзелСвойство(Узел, "Соседний") = Неопределено Тогда
			Текст = Текст + ПоказатьУзел(Данные, Запрос, Узел.Соседний, Уровень, Ложь);
		КонецЕсли;
	КонецЕсли;

	Возврат Текст;

КонецФункции // ПоказатьУзел()


Функция УзелПросмотр(Данные, Запрос,  КодУзла = Неопределено) Экспорт

	Если КодУзла = Неопределено Тогда
		КодУзла = Запрос.nodeid;
	КонецЕсли;

	Узел = Данные.ПолучитьУзел(КодУзла);
	Если Узел = Неопределено Тогда
		Возврат "Узел " + КодУзла + " не найден!";
	КонецЕсли;

	Текст = "";

	УзелПросмотр = УзелСостояние(Узел, "УзелПросмотр");
	Если УзелПросмотр = Истина Тогда
		ПараметрПредставление = ПоказатьУзел(Данные, Запрос, Узел.Код);
		ПараметрыШаблона = Новый Структура;
		ПараметрыШаблона.Вставить("ПараметрИдУзла", Узел.Код);
		ПараметрыШаблона.Вставить("ПараметрПредставление", ПараметрПредставление);
		Текст = Шаблон.ПолучитьОбласть("ОбластьПросмотр", ПараметрыШаблона);
	КонецЕсли;

	Возврат Текст;

КонецФункции // УзелПросмотр()


Функция ПоказатьСтруктуруУзла(Данные, Запрос, КодУзла = Неопределено, ПервыйВызов = Истина) Экспорт

	Представление = "";
	Атрибуты = "";

	Если КодУзла = Неопределено Тогда
		Если Запрос.Свойство("loadnodeid") Тогда
			КодУзла = Запрос.loadnodeid;
		Иначе
			КодУзла = Запрос.nodeid;
		КонецЕсли;
	КонецЕсли;

	Узел = Данные.ПолучитьУзел(КодУзла);
	Если Узел = Неопределено Тогда
		Возврат "Узел " + КодУзла + " не найден!";
	КонецЕсли;

	УзелОткрыт = УзелСостояние(Узел, "УзелОткрыт");
	Если УзелОткрыт = Неопределено Тогда
		УзелОткрыт = Ложь;
	КонецЕсли;

	УзелИмя = Узел.Имя;

	УзелЗначение = "";
	Если Узел.Свойство("Значение") Тогда
		УзелЗначение = Узел.Значение;
	КонецЕсли;

	ГлУзелИзменить = УзелСвойство(Узел, "mainodedit");
	УзелРедактируется = УзелСостояние(Узел, "УзелРедактируется");
	РедактироватьЗначение = УзелСостояние(Узел, "РедактироватьЗначение");
	УзелПросмотр = УзелСостояние(Узел, "УзелПросмотр");
	этоАтрибут = УзелСвойство(Узел, "attr");

	Если этоАтрибут = Истина Тогда
		УзелИмя = СтрЗаменить(УзелИмя, "xml_lang", "xml:lang");
		УзелИмя = СтрЗаменить(УзелИмя, "_", "-");
	КонецЕсли;

	КнопкаУзел = "";

	Если НЕ УзелСвойство(Узел, "Дочерний") = Неопределено Тогда
		ПараметрыШаблона = Новый Структура;
		ПараметрыШаблона.Вставить("ПараметрИдУзла", Узел.Код);
		ПараметрыШаблона.Вставить("ПараметрИдПроцесса", Запрос.procid);
		ПараметрыШаблона.Вставить("ПараметрРежим", Запрос.mode);
		ПараметрыШаблона.Вставить("ПараметрКоманда", ?(УзелОткрыт, "nodeclose", "nodeopen"));
		ПараметрыШаблона.Вставить("ПараметрНадписьНаКнопке", ?(УзелОткрыт, "⚪" , "⚫"));
		КнопкаУзел = Шаблон.ПолучитьОбласть("ОбластьКнопкаУзел", ПараметрыШаблона);
	КонецЕсли;

	КнопкиИнструменты = "";
	ПараметрМенюИнструменты = "";

	Если УзелРедактируется = Истина Тогда

		Родитель = Данные.Родитель(Узел);
		Если Родитель = Неопределено Тогда
			Родитель = Узел;
		КонецЕсли;

		ПараметрыШаблона = Новый Структура;
		ПараметрыШаблона.Вставить("ПараметрИдУзла", Узел.Код);
		ПараметрыШаблона.Вставить("ПараметрОбновитьУзел", Узел.Код);
		ПараметрыШаблона.Вставить("ПараметрНеактивно", "");
		ПараметрыШаблона.Вставить("ПараметрИдПроцесса", Запрос.procid);
		ПараметрыШаблона.Вставить("ПараметрРежим", Запрос.mode);
		ПараметрыШаблона.Вставить("ПараметрВидимость", "");

		Если НЕ этоАтрибут = Истина Тогда
			ПараметрыШаблона.Вставить("ПараметрКоманда", ?(УзелПросмотр = Истина, "nopreview", "preview"));
			ПараметрыШаблона.Вставить("ПараметрПодсказка", ?(УзелПросмотр = Истина, "Скрыть", "Показать"));
			ПараметрМенюИнструменты = ПараметрМенюИнструменты + Шаблон.ПолучитьОбласть("ОбластьМенюИнструменты", ПараметрыШаблона);

			ПараметрыШаблона.Вставить("ПараметрКоманда", "attradd");
			ПараметрыШаблона.Вставить("ПараметрПодсказка", "Новый атрибут");
			ПараметрМенюИнструменты = ПараметрМенюИнструменты + Шаблон.ПолучитьОбласть("ОбластьМенюИнструменты", ПараметрыШаблона);

			ПараметрыШаблона.Вставить("ПараметрКоманда", "childadd");
			ПараметрыШаблона.Вставить("ПараметрПодсказка", "Новый дочерний");
			ПараметрМенюИнструменты = ПараметрМенюИнструменты + Шаблон.ПолучитьОбласть("ОбластьМенюИнструменты", ПараметрыШаблона);
		КонецЕсли;

		ПараметрыШаблона.Вставить("ПараметрОбновитьУзел", Родитель.Код);
		ПараметрыШаблона.Вставить("ПараметрКоманда", "nextadd");
		ПараметрыШаблона.Вставить("ПараметрПодсказка", "Новый соседний");
		ПараметрМенюИнструменты = ПараметрМенюИнструменты + Шаблон.ПолучитьОбласть("ОбластьМенюИнструменты", ПараметрыШаблона);

		ПараметрыШаблона.Вставить("ПараметрКоманда", "nodecut");
		ПараметрыШаблона.Вставить("ПараметрПодсказка", "Вырезать узел");
		ПараметрМенюИнструменты = ПараметрМенюИнструменты + Шаблон.ПолучитьОбласть("ОбластьМенюИнструменты", ПараметрыШаблона);

		ПараметрыШаблона.Вставить("ПараметрОбновитьУзел", Узел.Код);
		ПараметрыШаблона.Вставить("ПараметрКоманда", "nodecopy");
		ПараметрыШаблона.Вставить("ПараметрПодсказка", "Копировать узел");
		ПараметрМенюИнструменты = ПараметрМенюИнструменты + Шаблон.ПолучитьОбласть("ОбластьМенюИнструменты", ПараметрыШаблона);

		Если НЕ этоАтрибут = Истина Тогда
			ПараметрыШаблона.Вставить("ПараметрОбновитьУзел", Родитель.Код);
			ПараметрыШаблона.Вставить("ПараметрКоманда", "nodepasteattr");
			ПараметрыШаблона.Вставить("ПараметрПодсказка", "Вставить атрибут");
			ПараметрМенюИнструменты = ПараметрМенюИнструменты + Шаблон.ПолучитьОбласть("ОбластьМенюИнструменты", ПараметрыШаблона);

			ПараметрыШаблона.Вставить("ПараметрОбновитьУзел", Узел.Код);
			ПараметрыШаблона.Вставить("ПараметрКоманда", "nodepastechild");
			ПараметрыШаблона.Вставить("ПараметрПодсказка", "Вставить дочерний");
			ПараметрМенюИнструменты = ПараметрМенюИнструменты + Шаблон.ПолучитьОбласть("ОбластьМенюИнструменты", ПараметрыШаблона);
		КонецЕсли;

		ПараметрыШаблона.Вставить("ПараметрОбновитьУзел", Родитель.Код);
		ПараметрыШаблона.Вставить("ПараметрКоманда", "nodepastenext");
		ПараметрыШаблона.Вставить("ПараметрПодсказка", "Вставить соседний");
		ПараметрМенюИнструменты = ПараметрМенюИнструменты + Шаблон.ПолучитьОбласть("ОбластьМенюИнструменты", ПараметрыШаблона);

		Если НЕ этоАтрибут = Истина Тогда
			ПараметрыШаблона.Вставить("ПараметрОбновитьУзел", Узел.Код);
			ПараметрыШаблона.Вставить("ПараметрКоманда", "attremove");
			ПараметрыШаблона.Вставить("ПараметрПодсказка", "Удалить все атрибуты");
			ПараметрМенюИнструменты = ПараметрМенюИнструменты + Шаблон.ПолучитьОбласть("ОбластьМенюИнструменты", ПараметрыШаблона);

			ПараметрыШаблона.Вставить("ПараметрКоманда", "childremove");
			ПараметрыШаблона.Вставить("ПараметрПодсказка", "Удалить все дочерние");
			ПараметрМенюИнструменты = ПараметрМенюИнструменты + Шаблон.ПолучитьОбласть("ОбластьМенюИнструменты", ПараметрыШаблона);
		КонецЕсли;

		ПараметрыШаблона.Вставить("ПараметрОбновитьУзел", Родитель.Код);
		ПараметрыШаблона.Вставить("ПараметрКоманда", "noderemove");
		ПараметрыШаблона.Вставить("ПараметрПодсказка", "Удалить узел");
		ПараметрМенюИнструменты = ПараметрМенюИнструменты + Шаблон.ПолучитьОбласть("ОбластьМенюИнструменты", ПараметрыШаблона);

		ПараметрыШаблона = Новый Структура;
		ПараметрыШаблона.Вставить("ПараметрИдУзла", Узел.Код);
		ПараметрыШаблона.Вставить("ПараметрМенюИнструменты", ПараметрМенюИнструменты);
		КнопкиИнструменты = Шаблон.ПолучитьОбласть("ОбластьКнопкаИнструменты", ПараметрыШаблона);

	КонецЕсли;

	Если УзелСвойство(Узел, "namedit") = Истина Тогда

		ПараметрыШаблона = Новый Структура;
		ПараметрыШаблона.Вставить("ПараметрИдУзла", Узел.Код);
		ПараметрыШаблона.Вставить("ПараметрИдПроцесса", Запрос.procid);
		ПараметрыШаблона.Вставить("ПараметрРежим", Запрос.mode);
		ПараметрыШаблона.Вставить("ПараметрКоманда", "submitname");
		ПараметрыШаблона.Вставить("ПараметрИмяЗначение", УзелИмя);
		ПараметрИмя = Шаблон.ПолучитьОбласть("ОбластьИзменитьИмяЗначение", ПараметрыШаблона);

	Иначе

		ПараметрыШаблона = Новый Структура;
		ПараметрыШаблона.Вставить("ПараметрИдУзла", Узел.Код);
		ПараметрыШаблона.Вставить("ПараметрНеактивно", "");
		ПараметрыШаблона.Вставить("ПараметрИдПроцесса", Запрос.procid);
		ПараметрыШаблона.Вставить("ПараметрРежим", Запрос.mode);
		ПараметрыШаблона.Вставить("ПараметрКоманда", ?(УзелРедактируется = Истина, "namedit", "nodedit"));
		ПараметрыШаблона.Вставить("ПараметрНадписьНаКнопке", УзелИмя);
		ПараметрИмя = Шаблон.ПолучитьОбласть("ОбластьКнопкаИмяЗначение", ПараметрыШаблона);

	КонецЕсли;

	Если РедактироватьЗначение = Истина Тогда

		ПараметрыШаблона = Новый Структура;
		ПараметрыШаблона.Вставить("ПараметрИдУзла", Узел.Код);
		ПараметрыШаблона.Вставить("ПараметрИдПроцесса", Запрос.procid);
		ПараметрыШаблона.Вставить("ПараметрРежим", Запрос.mode);
		ПараметрыШаблона.Вставить("ПараметрКоманда", "submitvalue");
		ПараметрыШаблона.Вставить("ПараметрИмяЗначение", УзелЗначение);
		ПараметрЗначение = Шаблон.ПолучитьОбласть("ОбластьИзменитьИмяЗначение", ПараметрыШаблона);

	ИначеЕсли НЕ УзелЗначение = "" ИЛИ (УзелРедактируется = Истина ИЛИ ГлУзелИзменить = Истина) Тогда

		ПараметрыШаблона = Новый Структура;
		ПараметрыШаблона.Вставить("ПараметрИдУзла", Узел.Код);
		ПараметрыШаблона.Вставить("ПараметрНеактивно", ?(УзелРедактируется = Истина, "", "disabled"));
		ПараметрыШаблона.Вставить("ПараметрИдПроцесса", Запрос.procid);
		ПараметрыШаблона.Вставить("ПараметрРежим", Запрос.mode);
		ПараметрыШаблона.Вставить("ПараметрКоманда", "valuedit");
		ПараметрыШаблона.Вставить("ПараметрНадписьНаКнопке", УзелЗначение);
		ПараметрЗначение = Шаблон.ПолучитьОбласть("ОбластьКнопкаИмяЗначение", ПараметрыШаблона);

	КонецЕсли;

	ПараметрыШаблона = Новый Структура;
	ПараметрыШаблона.Вставить("ПараметрИдУзла", Узел.Код);
	ПараметрыШаблона.Вставить("ПараметрИнструменты", КнопкиИнструменты);
	ПараметрыШаблона.Вставить("ПараметрИмя", ПараметрИмя);
	ПараметрыШаблона.Вставить("ПараметрЗначение", ПараметрЗначение);
	ПараметрИмяУзла = КнопкаУзел + Шаблон.ПолучитьОбласть("ОбластьИмяЗначение", ПараметрыШаблона);

	Если НЕ УзелСвойство(Узел, "Атрибут") = Неопределено Тогда
		УзелАтрибут = Данные.Атрибут(Узел);
		УзелАтрибут.Вставить("mainodedit", УзелРедактируется = Истина);
		УзелАтрибут.Вставить("attr", Истина);
		Атрибуты = ПоказатьСтруктуруУзла(Данные, Запрос, Узел.Атрибут, Ложь);
	КонецЕсли;

	Если этоАтрибут = Истина Тогда
		Если НЕ УзелСвойство(Узел, "Соседний") = Неопределено Тогда
			УзелАтрибут = Данные.Соседний(Узел);
			УзелАтрибут.Вставить("mainodedit", ГлУзелИзменить = Истина);
			УзелАтрибут.Вставить("attr", Истина);
			ПараметрИмяУзла = ПараметрИмяУзла + ПоказатьСтруктуруУзла(Данные, Запрос, Узел.Соседний, Ложь);
		КонецЕсли;

		Возврат ПараметрИмяУзла;
	КонецЕсли;

	ПараметрЗаголовокУзла = ПараметрИмяУзла + Атрибуты;

	ДочернийУзел = "";
	Если УзелОткрыт Тогда
		Если НЕ УзелСвойство(Узел, "Дочерний") = Неопределено Тогда
			ДочернийУзел = ПоказатьСтруктуруУзла(Данные, Запрос, Узел.Дочерний, Ложь);
		КонецЕсли;
	КонецЕсли;

	Если УзелПросмотр = Истина Тогда
		ДочернийУзел = ДочернийУзел + УзелПросмотр(Данные, Запрос, Узел.Код);
	КонецЕсли;

	ПараметрыШаблона = Новый Структура;
	ПараметрыШаблона.Вставить("ПараметрИдУзла", Узел.Код);
	ПараметрыШаблона.Вставить("ПараметрЗаголовокУзла", ПараметрЗаголовокУзла);
	ПараметрыШаблона.Вставить("ПараметрДочернийУзел", ДочернийУзел);
	Представление = Шаблон.ПолучитьОбласть("ОбластьУзел", ПараметрыШаблона);

	Если НЕ ПервыйВызов Тогда
		Если НЕ УзелСвойство(Узел, "Соседний") = Неопределено Тогда
				Представление = Представление + ПоказатьСтруктуруУзла(Данные, Запрос, Узел.Соседний, Ложь);
		КонецЕсли;
	КонецЕсли;

	Возврат Представление;

КонецФункции


Функция ОбработатьКоманду(структЗадача) Экспорт

	Запрос = структЗадача.Запрос;

	Если Запрос.cmd = "domupdate" Тогда
		структЗадача.Действие = Новый Структура("Имя, Узел, Результат", "ОбновитьDOM");
		Возврат "ВыполнитьДействия";
	КонецЕсли;

	Данные = структЗадача.Данные;
	КодУзла = Запрос.nodeid;
	Узел = Данные.ПолучитьУзел(КодУзла);
	Если Узел = Неопределено Тогда
		Сообщить ("Узел не найден!");
		Возврат "СформироватьОтвет";
	КонецЕсли;

	Если Запрос.cmd = "nodeopen" Тогда
		структЗадача.Действие = Новый Структура("Имя, Узел, Результат", "ОткрытьУзел", Узел);
		Если Запрос.mode = "lisp" Тогда
			Попытка
				Значение = Данные.Интерпретировать(Данные.Окружение, Узел);
			Исключение
				Значение = ОписаниеОшибки();
			КонецПопытки;
			Узел.Вставить("Значение", Значение);
		КонецЕсли;
	ИначеЕсли Запрос.cmd = "nodeclose" Тогда
		структЗадача.Действие = Новый Структура("Имя, Узел, Результат", "ЗакрытьУзел", Узел);
	ИначеЕсли Запрос.cmd = "nodedit" Тогда
		структЗадача.Действие = Новый Структура("Имя, Узел, Результат", "РедактироватьУзел", Узел);
	ИначеЕсли Запрос.cmd = "preview" Тогда
		структЗадача.Действие = Новый Структура("Имя, Узел, Результат", "ПоказатьУзел", Узел);
	ИначеЕсли Запрос.cmd = "nopreview" Тогда
		структЗадача.Действие = Новый Структура("Имя, Узел, Результат", "СкрытьУзел", Узел);
	ИначеЕсли Запрос.cmd = "nodereload" Тогда
		структЗадача.Действие = Новый Структура("Имя, Узел, Результат", "ОбновитьУзел", Узел);
	ИначеЕсли Запрос.cmd = "attradd" Тогда
		СтруктураУзла = Новый Структура("Имя, Значение, Старший, namedit, valuedit", "", "", Узел.Код, Истина, Истина);
		УзелСоседний = Данные.Атрибут(Узел);
		Если НЕ УзелСоседний = Неопределено Тогда
			СтруктураУзла.Вставить("Соседний", УзелСоседний.Код);
		КонецЕсли;
		НовыйУзел = Данные.НовыйУзел(СтруктураУзла);
		НовыйУзел.Вставить("attr", Истина);
		УзелСостояниеЗначение(НовыйУзел, "ТребуетсяРедактировать", Истина);
		Узел.Вставить("Атрибут", НовыйУзел.Код);
		Если НЕ УзелСоседний = Неопределено Тогда
			УзелСоседний.Вставить("Старший", НовыйУзел.Код);
		КонецЕсли;
	ИначеЕсли Запрос.cmd = "childadd" Тогда
		СтруктураУзла = Новый Структура("Имя, Значение, Старший, namedit, valuedit", "", "", Узел.Код, Истина, Истина);
		УзелСоседний = Данные.Дочерний(Узел);
		Если НЕ УзелСоседний = Неопределено Тогда
			СтруктураУзла.Вставить("Соседний", УзелСоседний.Код);
		КонецЕсли;
		НовыйУзел = Данные.НовыйУзел(СтруктураУзла);
		УзелСостояниеЗначение(НовыйУзел, "ТребуетсяРедактировать", Истина);
		УзелСостояниеЗначение(НовыйУзел, "ТребуетсяОткрыть", Истина);
		УзелСостояниеЗначение(Узел, "ТребуетсяОткрыть", Истина);
		Узел.Вставить("Дочерний", НовыйУзел.Код);
		Если НЕ УзелСоседний = Неопределено Тогда
			УзелСоседний.Вставить("Старший", НовыйУзел.Код);
		КонецЕсли;
	ИначеЕсли Запрос.cmd = "nextadd" Тогда
		СтруктураУзла = Новый Структура("Имя, Значение, Старший, namedit, valuedit", "", "", Узел.Код, Истина, Истина);
		УзелСоседний = Данные.Соседний(Узел);
		Если НЕ УзелСоседний = Неопределено Тогда
			СтруктураУзла.Вставить("Соседний", УзелСоседний.Код);
		КонецЕсли;
		НовыйУзел = Данные.НовыйУзел(СтруктураУзла);
		УзелСостояниеЗначение(НовыйУзел, "ТребуетсяРедактировать", Истина);
		Узел.Вставить("Соседний", НовыйУзел.Код);
		Если НЕ УзелСоседний = Неопределено Тогда
			УзелСоседний.Вставить("Старший", НовыйУзел.Код);
		КонецЕсли;
	ИначеЕсли Запрос.cmd = "noderemove" Тогда
		Данные.УдалитьУзел(Узел);
	ИначеЕсли Запрос.cmd = "nodecopy" Тогда
		Если НЕ Буфер = Неопределено Тогда
			ОсвободитьОбъект(Буфер);
		КонецЕсли;
		Буфер = Новый Соответствие;
		БуферУзел = Узел.Код;
		Данные.КопироватьУзел(Узел, Буфер);
	ИначеЕсли Запрос.cmd = "nodecut" Тогда
		Если НЕ Буфер = Неопределено Тогда
			ОсвободитьОбъект(Буфер);
		КонецЕсли;
		Буфер = Новый Соответствие;
		БуферУзел = Узел.Код;
		Данные.КопироватьУзел(Узел, Буфер);
		Данные.УдалитьУзел(Узел, Ложь);
	ИначеЕсли Запрос.cmd = "nodepasteattr" Тогда
		Если НЕ Буфер = Неопределено Тогда
			НовыйУзел = Данные.ВставитьУзел(Буфер, БуферУзел, Истина);
			НовыйУзел.Вставить("Старший", Узел.Код);
			НовыйУзел.Вставить("Атрибут", Неопределено);
			НовыйУзел.Вставить("Дочерний", Неопределено);
			УзелАтрибут = Данные.Атрибут(Узел);
			Узел.Вставить("Атрибут", НовыйУзел.Код);
			Если НЕ УзелАтрибут = Неопределено Тогда
				НовыйУзел.Вставить("Соседний", УзелАтрибут.Код);
				УзелАтрибут.Вставить("Старший", НовыйУзел.Код);
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли Запрос.cmd = "nodepastechild" Тогда
		Если НЕ Буфер = Неопределено Тогда
			НовыйУзел = Данные.ВставитьУзел(Буфер, БуферУзел);
			НовыйУзел.Вставить("Старший", Узел.Код);
			УзелДочерний = Данные.Дочерний(Узел);
			Узел.Вставить("Дочерний", НовыйУзел.Код);
			Если НЕ УзелДочерний = Неопределено Тогда
				НовыйУзел.Вставить("Соседний", УзелДочерний.Код);
				УзелДочерний.Вставить("Старший", НовыйУзел.Код);
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли Запрос.cmd = "nodepastenext" Тогда
		Если НЕ Буфер = Неопределено Тогда
			НовыйУзел = Данные.ВставитьУзел(Буфер, БуферУзел);
			НовыйУзел.Вставить("Старший", Узел.Код);
			УзелСоседний = Данные.Соседний(Узел);
			Узел.Вставить("Соседний", НовыйУзел.Код);
			Если НЕ УзелСоседний = Неопределено Тогда
				НовыйУзел.Вставить("Соседний", УзелСоседний.Код);
				УзелСоседний.Вставить("Старший", НовыйУзел.Код);
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли Запрос.cmd = "attremove" Тогда
		Если НЕ УзелСвойство(Узел, "Атрибут") = Неопределено Тогда
			Данные.УдалитьУзел(Данные.Атрибут(Узел), Ложь);
			//Узел.Удалить("Атрибут");
			Узел.Атрибут = Неопределено;
		КонецЕсли
	ИначеЕсли Запрос.cmd = "childremove" Тогда
		Если НЕ УзелСвойство(Узел, "Дочерний") = Неопределено Тогда
			Данные.УдалитьУзел(Данные.Дочерний(Узел), Ложь);
			//Узел.Удалить("Дочерний");
			Узел.Дочерний = Неопределено;
		КонецЕсли
	ИначеЕсли Запрос.cmd = "namedit" Тогда
		Узел.Вставить("namedit", Истина);
	ИначеЕсли Запрос.cmd = "valuedit" Тогда
		структЗадача.Действие = Новый Структура("Имя, Узел, Результат", "РедактироватьЗначение", Узел);
	ИначеЕсли Запрос.cmd = "submitvalue" Тогда
		структЗадача.Действие = Новый Структура("Имя, Узел, НовоеЗначение, Результат", "НовоеЗначениеУзла", Узел, Неопределено);
		Если Запрос.Свойство("valuedit") Тогда
			структЗадача.Действие.Вставить("НовоеЗначение", Запрос.valuedit);
		КонецЕсли;
	ИначеЕсли Запрос.cmd = "submitname" Тогда
		Узел.Вставить("namedit", Ложь);
		Если Запрос.Свойство("valuedit") Тогда
			Узел.Вставить("Имя", Запрос.valuedit);
		КонецЕсли;
	КонецЕсли;

	Возврат "ВыполнитьДействия";
КонецФункции // ОбработатьКоманду()


Функция ЗапросВыполнитьЗадачу(структЗадача)

	Если структЗадача.Этап = "ВыполнитьЗадачу" Тогда
		Запрос = структЗадача.Запрос;

		Если Запрос.Свойство("data") Тогда
			data = Запрос.data;
		КонецЕсли;

		Если data = Неопределено Тогда
			структЗадача.Результат = НачальнаяСтраница();
			структЗадача.Этап = "ЕстьРезультат";
		ИначеЕсли data = "newpage" Тогда
			структЗадача.Результат = НоваяСтраница();
			структЗадача.Этап = "ЕстьРезультат";
		Иначе
			структВкладка = Вкладки.Получить(data);
			Если НЕ структВкладка = Неопределено Тогда
				Данные = структВкладка.Данные;
			Иначе
				Данные = Новый pagedata("" + data + ".txt", Шаблон);
				структВкладка = Новый Структура("Данные, Режим, ИдУзла, Заголовок", Данные, Запрос.mode, Запрос.nodeid, Запрос.descr);
				Вкладки.Вставить("" + data, структВкладка);
			КонецЕсли;
			структЗадача.Вставить("Данные", Данные);
			Если Запрос.Свойство("cmd") Тогда
				структЗадача.Этап = ОбработатьКоманду(структЗадача);
			Иначе
				структЗадача.Этап = "СформироватьОтвет";
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

	Если структЗадача.Этап = "ВыполнитьДействия" Тогда
		Если ВыполнитьДействия(структЗадача) = Истина Тогда
			структЗадача.Этап = "СформироватьОтвет";
		КонецЕсли;
	КонецЕсли;

	Если структЗадача.Этап = "СформироватьОтвет" Тогда
		Ответ = "";
		Запрос = структЗадача.Запрос;
		Данные = структЗадача.Данные;
		Если Запрос.Свойство("mode") Тогда
			Если Запрос.mode = "design" Тогда
				Ответ = ПоказатьСтруктуруУзла(Данные, Запрос);
			ИначеЕсли Запрос.mode = "lisp" Тогда
				Ответ = ПоказатьСтруктуруУзла(Данные, Запрос);
			Иначе
				Ответ = ПоказатьУзел(Данные, Запрос);
			КонецЕсли;
		Иначе
			Ответ = УзелПросмотр(Данные, Запрос);
		КонецЕсли;
		структЗадача.Результат = Ответ;
		структЗадача.Этап = "ЕстьРезультат";
	КонецЕсли;

	Если структЗадача.Этап = "ЕстьРезультат" Тогда
		Соединение = Неопределено;
		Если ПередатьСтроку(Соединение, "<!--" + структЗадача.Запрос.taskid + "-->" + структЗадача.Результат) Тогда
			Если НЕ Соединение = Неопределено Тогда
				Соединение.Закрыть();
				структЗадача.Этап = "УдалитьЗадачу";
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

КонецФункции // ЗапросВыполнитьЗадачу()


Функция УзелСвойство(Узел, Свойство) Экспорт
	УзелСвойство = Неопределено;
	Если НЕ Узел = Неопределено Тогда
		Узел.Свойство(Свойство, УзелСвойство);
	КонецЕсли;
	Возврат УзелСвойство;
КонецФункции // УзелСвойство(Узел)


Функция УзелСвойствоЗначение(Узел, СвойствоИмя, СвойствоЗначение) Экспорт
	Если НЕ Узел = Неопределено Тогда
		Узел.Вставить(СвойствоИмя, СвойствоЗначение);
	КонецЕсли;
	Возврат Неопределено;
КонецФункции // УзелСвойствоЗначение(Узел)


Функция УзелСостояние(Узел, СостояниеИмя = Неопределено) Экспорт
	УзелСостояние = Неопределено;
	Если НЕ Узел = Неопределено Тогда
		Узел.Свойство("Состояние", УзелСостояние);
		Если НЕ УзелСостояние = Неопределено Тогда
			Если НЕ СостояниеИмя = Неопределено Тогда
				УзелСостояние.Свойство(СостояниеИмя, УзелСостояние);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	Возврат УзелСостояние;
КонецФункции // УзелСостояние(Узел)


Функция УзелСостояниеЗначение(Узел, СостояниеИмя, СостояниеЗначение, Событие = Истина) Экспорт
	Если НЕ Узел = Неопределено Тогда
		Если УзелСвойство(Узел, "Состояние") = Неопределено Тогда
			Узел.Вставить("Состояние", Новый Структура());
		КонецЕсли;
		Узел.Состояние.Вставить(СостояниеИмя, СостояниеЗначение);
		Если Событие Тогда
			Сообщить(СостояниеИмя + "=" + СостояниеЗначение);
			//НовоеСобытие(Новый Структура("Имя, Узел, СостояниеИмя", "ОбновитьСостояние", Узел, СостояниеИмя));
		КонецЕсли;
	КонецЕсли;
	Возврат Неопределено;
КонецФункции // УзелСостояниеЗначение(Узел)


Функция НовоеСобытие(СтруктураСобытия)
	ИдСобытия = ИдСобытия + 1;
	Событие = Новый Структура(СтруктураСобытия);
	События.Вставить(ИдСобытия, Событие);
	Возврат Событие;
КонецФункции // НовоеСобытие()


Функция ОбработатьСобытия(Данные)
	Для каждого элСобытие Из События Цикл
		Событие = элСобытие.Значение;
		Если Событие.Имя = "ОбновитьСостояние" Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
КонецФункции // ОбработатьСобытия()


Функция ОбновитьСостояние(Данные, Узел, Состояние, Значение)

	Если Узел = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;

	Результат = Истина;

	Если Состояние = "УзелПросмотр" И Значение = Истина Тогда
		Результат = ОбновитьСостояние(Данные, Узел, "УзелОткрыт", Истина);

	ИначеЕсли Состояние = "УзелПросмотр" И Значение = Ложь Тогда
		ОбновитьСостояние(Данные, Узел, "ОбновитьУзел", Истина);

	ИначеЕсли Состояние = "НовоеЗначениеУзла" Тогда
		ОбновитьСостояние(Данные, Узел, "РедактироватьЗначение", Ложь);
		УзелСвойствоЗначение(Узел, "Значение", Значение);
		ОбновитьСостояние(Данные, Узел, "ОбновитьУзел", Истина);

	ИначеЕсли Состояние = "ОбновитьУзел" И Значение = Истина Тогда
		ОбновитьСостояние(Данные, Данные.Родитель(Узел), "ОбновитьУзел", Истина);
		Результат = (УзелСостояние(Узел, "УзелПросмотр") = Истина);

	ИначеЕсли Состояние = "ОбновитьУзел" И Значение = Ложь Тогда
		Результат = УзелСостояние(Узел, "ОбновитьУзел");

	КонецЕсли;

	Если НЕ Результат = Ложь Тогда
		УзелСостояниеЗначение(Узел, Состояние, Значение);
	КонецЕсли;

	Возврат Результат;

КонецФункции // ОбновитьСостояние()


Функция ПолучитьИзмененныйУзел(Данные)
	Для каждого элУзел Из Данные.Узлы Цикл
		Узел = элУзел.Значение;
		Если УзелСостояние(Узел, "ОбновитьУзел") = Истина Тогда
			Если ОбновитьСостояние(Данные, Узел, "ОбновитьУзел", Ложь) = Истина Тогда
				Возврат Узел;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	Возврат Неопределено;
КонецФункции // ПолучитьИзмененныйУзел()


Функция ВыполнитьДействия(структЗадача)
	Действие = УзелСвойство(структЗадача, "Действие");
	Если НЕ Действие = Неопределено Тогда

		Данные = структЗадача.Данные;

		Если Действие.Имя = "ОбновитьDOM" Тогда
			Узел = ПолучитьИзмененныйУзел(Данные);
			Если НЕ Узел = Неопределено Тогда
				структЗадача.Запрос.Вставить("nodeid", Узел.Код);
				Возврат Истина;
			КонецЕсли;
			Возврат Ложь;
		КонецЕсли;

		Узел = Действие.Узел;

		Если Действие.Имя = "ОткрытьУзел" Тогда
			Возврат ОбновитьСостояние(Данные, Узел, "УзелОткрыт", Истина);

		ИначеЕсли Действие.Имя = "ЗакрытьУзел" Тогда
			Возврат ОбновитьСостояние(Данные, Узел, "УзелОткрыт", Ложь);

		ИначеЕсли Действие.Имя = "РедактироватьУзел" Тогда
			Возврат ОбновитьСостояние(Данные, Узел, "УзелРедактируется", Истина);

		ИначеЕсли Действие.Имя = "РедактироватьЗначение" Тогда
			Возврат ОбновитьСостояние(Данные, Узел, "РедактироватьЗначение", Истина);

		ИначеЕсли Действие.Имя = "НовоеЗначениеУзла" Тогда
			Возврат ОбновитьСостояние(Данные, Узел, "НовоеЗначениеУзла", Действие.НовоеЗначение);

		ИначеЕсли Действие.Имя = "ПоказатьУзел" Тогда
			Возврат ОбновитьСостояние(Данные, Узел, "УзелПросмотр", Истина);

		ИначеЕсли Действие.Имя = "СкрытьУзел" Тогда
			Возврат ОбновитьСостояние(Данные, Узел, "УзелПросмотр", Ложь);

		ИначеЕсли Действие.Имя = "ОбновитьУзел" Тогда
			Возврат ОбновитьСостояние(Данные, Узел, "ОбновитьУзел", Ложь);

		КонецЕсли;
	КонецЕсли;

	Возврат Ложь;
КонецФункции // ВыполнитьДействия()


Функция ПередатьСтроку(Соединение, СтрокаДанные)
	Попытка
		Соединение = Новый TCPСоединение(Хост, Порт);
		Соединение.ТаймаутОтправки = 100;
		Соединение.ОтправитьСтроку(СтрокаДанные);
		Возврат Истина;
	Исключение
		Соединение = Неопределено;
		Возврат Ложь;
	КонецПопытки;
КонецФункции // ПередатьСтроку()



Если АргументыКоманднойСтроки.Количество() Тогда
	procid = АргументыКоманднойСтроки[0];
Иначе
	procid = "1";
КонецЕсли;

Шаблон = ЗагрузитьСценарий(ОбъединитьПути(ТекущийКаталог(), "template.os"));
Шаблон.ЗагрузитьМакет("showdata");

//ПодключитьСценарий(ОбъединитьПути(ТекущийКаталог(), "dbaccess.os"), "dbaccess");
ПодключитьСценарий(ОбъединитьПути(ТекущийКаталог(), "pagedata.os"), "pagedata");

Вкладки = Новый Соответствие;
Буфер = Неопределено;

События = Новый Соответствие;
ИдСобытия = 0;

Таймаут = 10;
Хост = "127.0.0.1";
Порт = 8888;
Соединение = Неопределено;

Задачи = Новый Соответствие;

СтруктЗапрос = СтруктуруВСтроку(Новый Структура("procid", procid));
КоличествоПопыток = 100;

Попытка

	Пока Истина Цикл

		Для каждого элЗадача Из Задачи Цикл
			структЗадача = ЭлЗадача.Значение;
			Если структЗадача.Тип = "Запрос" Тогда
				ЗапросВыполнитьЗадачу(структЗадача);
			КонецЕсли;
			Если структЗадача.Этап = "УдалитьЗадачу" Тогда
				Задачи.Удалить(элЗадача.Ключ);
				Прервать;
			КонецЕсли;
		КонецЦикла;

		Если Соединение = Неопределено Тогда
			Если НЕ ПередатьСтроку(Соединение, СтруктЗапрос) Тогда
				КоличествоПопыток = КоличествоПопыток - 1;
				Если КоличествоПопыток = 0 Тогда
					Сообщить("Хост недоступен");
					Прервать;
				КонецЕсли;
				Продолжить;
			КонецЕсли;
			КоличествоПопыток = 100;
		Иначе
			Если НЕ Соединение.Активно Тогда
				Соединение = Неопределено;
				Продолжить;
			КонецЕсли;
		КонецЕсли;

		Попытка
			ЗадачиКоличество = Задачи.Количество();
			Если ЗадачиКоличество Тогда
				Соединение.ТаймаутЧтения = 100;
			КонецЕсли;
			Запрос = Соединение.ПрочитатьСтроку();
		Исключение
			//Сообщить("Осталось задач: " + ЗадачиКоличество);
			Продолжить;
		КонецПопытки;

		Попытка
			Запрос = СтрокуВСтруктуру(Запрос);
			структЗадача = Новый Структура("Тип, Этап, Запрос, Действие, Результат", "Запрос", "ВыполнитьЗадачу", Запрос);
		Исключение
			структЗадача = Новый Структура("Тип, Этап, Запрос, Результат", "Запрос", "ЕстьРезультат", Неопределено, "Неверный запрос");
		КонецПопытки;

		Задачи.Вставить(Запрос.taskid, структЗадача);

		Соединение.Закрыть();
		Соединение = Неопределено;

	КонецЦикла;

Исключение
	Сообщить(ОписаниеОшибки());
КонецПопытки;
