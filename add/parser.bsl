Перем КодУзла;

Функция ВыгрузитьУзел(Узел, Знач Уровень)

	Перем Элементы;

	Если НЕ ТипЗнч(Узел) = Тип("Структура") Тогда
		Если ТипЗнч(Узел) = Тип("Строка") Тогда
			Кавычки = """";
		Иначе
			Кавычки = "";
		КонецЕсли;
		Возврат Кавычки +  Узел + Кавычки + ";";
	КонецЕсли;

	Элементы = Уровень + "Новый Структура(";
	Запятая = "";
	Кавычка = """";
	Значения = "";
	Уровень = Уровень + " ";

	Для каждого Элемент Из Узел Цикл

			Если ТипЗнч(Элемент.Значение) = Тип("Строка") Тогда
				Кавычки = """";
			Иначе
				Кавычки = "";
			КонецЕсли;
			Элементы = Элементы + Кавычка + Запятая + Элемент.Ключ;
			Если ТипЗнч(Элемент.Значение) = Тип("Структура") Тогда
				Значения = Значения + Запятая + Символы.ПС + ВыгрузитьУзел(Элемент.Значение, Уровень) + Символы.ПС + Уровень + ")";
			Иначе
				Значение = Элемент.Значение;
				Если Значение = Неопределено Тогда
					Значение = "Неопределено";
				КонецЕсли;
				Значения = Значения + Запятая + Символы.ПС + Уровень + Кавычки +  Значение + Кавычки;
			КонецЕсли;
			Запятая = ", ";
			Кавычка = "";

	КонецЦикла;

	Если НЕ Значения = "" Тогда
		Элементы = Элементы + """," + Значения;
	КонецЕсли;

	Возврат Элементы;

КонецФункции // ВыгрузитьУзел()


Функция СтруктуруВСтрокуВнутр(Объект)

	ДанныеЗаг = "";
	ДанныеТело = "";

	Для каждого элОбъект Из Объект Цикл
		ДанныеЗаг = ДанныеЗаг + "
		|Перем " +  элОбъект.Ключ + " Экспорт;";
		ДанныеТело = ДанныеТело + "
		|" + элОбъект.Ключ + " = " + ВыгрузитьУзел(элОбъект.Значение, "") + "
		|);";
	КонецЦикла;

	Возврат ДанныеЗаг + ДанныеТело;

КонецФункции // СтруктуруВСтрокуВнутр()


Функция ОбойтиУзел(Узел)

	Данные = Новый Структура("КодУзла",  КодУзла);
	КодУзла = СтрЗаменить(Строка(Число(КодУзла) + 1), Символы.НПП, "");

	Если Узел.ИмяУзла = "#text" ИЛИ Узел.ИмяУзла = "#comment" Тогда
		Если НЕ СокрЛП(Узел.ТекстовоеСодержимое) = "" Тогда
			Данные.Вставить("ИмяУзла", Узел.ИмяУзла);
			УзелТекстовоеСодержимое = Узел.ТекстовоеСодержимое;
			УзелТекстовоеСодержимое = СтрЗаменить(УзелТекстовоеСодержимое, Символы.ПС, "");
			УзелТекстовоеСодержимое = СтрЗаменить(УзелТекстовоеСодержимое, Символы.ВК, "");
			УзелТекстовоеСодержимое = СтрЗаменить(УзелТекстовоеСодержимое, """", """""");
			Данные.Вставить("Содержимое", СокрЛп(УзелТекстовоеСодержимое));
		Иначе
			Если НЕ Узел.СледующийСоседний = Неопределено Тогда
				Возврат ОбойтиУзел(Узел.СледующийСоседний);
			КонецЕсли;
			Возврат Неопределено;
		КонецЕсли;
	Иначе
		Данные.Вставить("ИмяУзла", Узел.ИмяУзла);

		Если НЕ Узел.Атрибуты = Неопределено Тогда
			Если Узел.Атрибуты.Количество() Тогда

				нАтрибут = Неопределено;

				Для каждого Атрибут Из Узел.Атрибуты Цикл
					Если НЕ нАтрибут = Неопределено Тогда
						нАтрибут.Вставить("СоседнийУзел", Новый Структура);
						нАтрибут = нАтрибут.СоседнийУзел;
					Иначе
						нАтрибут = Новый Структура;
						Данные.Вставить("Атрибуты", нАтрибут);
					КонецЕсли;
					нАтрибут.Вставить("КодУзла",  КодУзла);
					КодУзла = СтрЗаменить(Строка(Число(КодУзла) + 1), Символы.НПП, "");

					АтрибутИмя = Атрибут.Имя;
					АтрибутИмя = СтрЗаменить(АтрибутИмя, "xml:lang", "xml_lang");
					АтрибутИмя = СтрЗаменить(АтрибутИмя, "-", "_");
					АтрибутЗначение = Атрибут.Значение;
					АтрибутЗначение = СтрЗаменить(АтрибутЗначение, """", """""");
					АтрибутЗначение = СтрЗаменить(АтрибутЗначение, Символы.ПС, "");
					АтрибутЗначение = СтрЗаменить(АтрибутЗначение, Символы.ВК, "");

					нАтрибут.Вставить("ИмяУзла", АтрибутИмя);
					нАтрибут.Вставить("Содержимое", АтрибутЗначение);

			    КонецЦикла;

			КонецЕсли;

		КонецЕсли;

	КонецЕсли;

	Если НЕ Узел.ПервыйДочерний = Неопределено Тогда
		нДанные = ОбойтиУзел(Узел.ПервыйДочерний);
		Если НЕ нДанные = Неопределено Тогда
			Данные.Вставить("ДочернийУзел", нДанные);
		КонецЕсли;
	КонецЕсли;

	Если НЕ Узел.СледующийСоседний = Неопределено Тогда
		нДанные = ОбойтиУзел(Узел.СледующийСоседний);
		Если НЕ нДанные = Неопределено Тогда
			Данные.Вставить("СоседнийУзел", нДанные);
		КонецЕсли;
	КонецЕсли;

	Возврат Данные;

КонецФункции

ЧтениеHTML = Новый ЧтениеHTML;
ЧтениеHTML.ОткрытьФайл("Slate.htm", "UTF-8");
ПостроительDOM = Новый ПостроительDOM;
ДокументHTML = ПостроительDOM.Прочитать(ЧтениеHTML);

КодУзла = "0";

Данные = Новый Структура("Данные", ОбойтиУзел(ДокументHTML.ПервыйДочерний));
Данные.Вставить("КодУзла", КодУзла);

ФайлДанных = Новый ТекстовыйДокумент;
ФайлДанных.УстановитьТекст(СтруктуруВСтрокуВнутр(Данные));
ФайлДанных.Записать("slate.os");
