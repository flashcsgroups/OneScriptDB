Перем Данные;
Перем Шаблон;
Перем КодУзла;

Функция НайтиУзел(Запрос, Узел = Неопределено, Код = "") Экспорт

	Если Узел = Неопределено Тогда
		Узел = Данные;
	КонецЕсли;

	Если Запрос.nodeid = Код + Узел.КодУзла ИЛИ Запрос.nodeid = "0" Тогда
		Возврат Новый Структура("Узел, Код", Узел, Код);
	КонецЕсли;

	НайтиНомер = Сред(Запрос.nodeid, СтрДлина(Код) + 1);
	н = Найти(НайтиНомер, "_");
	Если н Тогда
		НайтиНомер = Лев(НайтиНомер, н - 1);
	КонецЕсли;

	Если НайтиНомер = Узел.КодУзла Тогда
		Если Узел.Свойство("ДочернийУзел") Тогда
			Возврат НайтиУзел(Запрос, Узел.ДочернийУзел, Код + Узел.КодУзла + "_");
		КонецЕсли;
	ИначеЕсли Узел.Свойство("Атрибуты") Тогда
		Если НайтиНомер = Узел.Атрибуты.КодУзла Тогда
			Возврат НайтиУзел(Запрос, Узел.Атрибуты, Код + Узел.Атрибуты.КодУзла + "_");
		КонецЕсли;
	КонецЕсли;

	Если Узел.Свойство("СоседнийУзел") Тогда
		Возврат НайтиУзел(Запрос, Узел.СоседнийУзел, Код);
	КонецЕсли;

КонецФункции // НайтиУзел()


Функция ПоказатьУзел(Запрос, стрУзел = Неопределено, Уровень = "") Экспорт

	Если стрУзел = Неопределено Тогда
		стрУзел = НайтиУзел(Запрос);
		Если стрУзел = Неопределено Тогда
			Возврат "Узел не найден!";
		КонецЕсли;
	КонецЕсли;

	Узел = стрУзел.Узел;

	Текст = Символы.ПС + Уровень;

	Если Узел.ИмяУзла = "#text" Тогда
		Если Узел.Свойство("Содержимое") Тогда
			Текст = Текст + Узел.Содержимое;
		Иначе
			Текст = "";
		КонецЕсли;
	ИначеЕсли Узел.ИмяУзла = "#comment" Тогда
		Если Узел.Свойство("Содержимое") Тогда
			Текст = Текст + "<!-- " + Узел.Содержимое + " -->";
		КонецЕсли;
	Иначе
		Текст = Текст + "<" + Узел.ИмяУзла;

		Атрибут = Неопределено;
		Если Узел.Свойство("Атрибуты", Атрибут) Тогда
			Пока НЕ Атрибут = Неопределено Цикл
				АтрибутИмя = Атрибут.ИмяУзла;
				АтрибутИмя = СтрЗаменить(АтрибутИмя, "xml_lang", "xml:lang");
				АтрибутИмя = СтрЗаменить(АтрибутИмя, "_", "-");
				АтрибутЗначение = Атрибут.Содержимое;
				Текст = Текст + " " + АтрибутИмя + "=""" + АтрибутЗначение + """";
				Атрибут.Свойство("СоседнийУзел", Атрибут);
		    КонецЦикла;
		КонецЕсли;

		Текст = Текст + ">";
	КонецЕсли;

	Если Узел.Свойство("ДочернийУзел") Тогда
		Текст = Текст + ПоказатьУзел(Запрос, Новый Структура("Узел, Код", Узел.ДочернийУзел, стрУзел.Код + Узел.КодУзла + "_"), Уровень + Символы.Таб);
	КонецЕсли;

	Если НЕ (Узел.ИмяУзла = "#text" ИЛИ Узел.ИмяУзла = "#comment") Тогда
		Текст = Текст + Символы.ПС + Уровень + "</" + Узел.ИмяУзла + ">";
	КонецЕсли;

	Если НЕ Уровень = "" Тогда
		Если Узел.Свойство("СоседнийУзел") Тогда
			Если Узел.ИмяУзла = "#comment" Тогда
				Если Узел.Свойство("Содержимое") Тогда
					Параметры = Новый Структура("ПараметрИдУзла, ПараметрИдПроцесса, ПараметрНадписьНаКнопке",
													стрУзел.Код + Узел.СоседнийУзел.КодУзла,
													Запрос.procid,
													Узел.Содержимое);
					Текст = Текст + Шаблон.ПолучитьОбласть("ОбластьКнопка", Параметры);
				КонецЕсли;
				Если Узел.СоседнийУзел.Свойство("СоседнийУзел") Тогда
					Текст = Текст + ПоказатьУзел(Запрос, Новый Структура("Узел, Код", Узел.СоседнийУзел.СоседнийУзел, стрУзел.Код), Уровень);
				КонецЕсли;
				Возврат Текст;
			КонецЕсли;

			Текст = Текст + ПоказатьУзел(Запрос, Новый Структура("Узел, Код", Узел.СоседнийУзел, стрУзел.Код), Уровень);
		КонецЕсли;
	КонецЕсли;

	Возврат Текст;

КонецФункции // ПоказатьУзел()

Функция ПоказатьСтруктуруУзла(Запрос, стрУзел = Неопределено) Экспорт

	Если НЕ Запрос.Свойство("cmd") Тогда
		Запрос.Вставить("cmd", "");
	КонецЕсли;

	Представление = "";
	Атрибуты = "";

	ПервыйВызов = (стрУзел = Неопределено);

	Если ПервыйВызов Тогда

		стрУзел = НайтиУзел(Запрос);
		Если стрУзел = Неопределено Тогда
			Возврат "Узел не найден!";
		КонецЕсли;

	КонецЕсли;

	Узел = стрУзел.Узел;
	УзелИмяУзла = Узел.ИмяУзла;

	Если Узел.Свойство("Атрибуты") Тогда
		Атрибуты = ПоказатьСтруктуруУзла(Запрос, Новый Структура("Узел, Код", Узел.Атрибуты, стрУзел.Код + Узел.Атрибуты.КодУзла + "_"));
	КонецЕсли;

	Если Узел.Свойство("Содержимое") Тогда
		Если Узел.ИмяУзла = "#text" ИЛИ Узел.ИмяУзла = "#comment" Тогда
			УзелИмяУзла = УзелИмяУзла + " = " + Узел.Содержимое;
		Иначе
			УзелИмяУзла = СтрЗаменить(УзелИмяУзла, "xml_lang", "xml:lang");
			УзелИмяУзла = СтрЗаменить(УзелИмяУзла, "_", "-");
			УзелИмяУзла = УзелИмяУзла + " = " + Узел.Содержимое;

			Параметры = Новый Структура("ПараметрИдУзла, ПараметрИдПроцесса, ПараметрКоманда, ПараметрНадписьНаКнопке",
				стрУзел.Код + Узел.КодУзла,
				Запрос.procid,
				"propedit",
				УзелИмяУзла);
			Представление = Шаблон.ПолучитьОбласть("ОбластьКнопкаСвойство", Параметры);

			Если Узел.Свойство("СоседнийУзел") Тогда
				Представление = Представление + ПоказатьСтруктуруУзла(Запрос, Новый Структура("Узел, Код", Узел.СоседнийУзел, стрУзел.Код));
			КонецЕсли;

			Возврат Представление;

		КонецЕсли;
	КонецЕсли;

	ДочернийУзел = "";
	Если ПервыйВызов И НЕ Запрос.cmd = "nodeclose" Тогда
		Если Узел.Свойство("ДочернийУзел") Тогда
			ДочернийУзел = ДочернийУзел + ПоказатьСтруктуруУзла(Запрос,
				Новый Структура("Узел, Код",
					Узел.ДочернийУзел,
					стрУзел.Код + Узел.КодУзла + "_"));
		КонецЕсли;
	КонецЕсли;

	ПараметрИмяУзла = Шаблон.ПолучитьОбласть("ОбластьКнопкаУзел",
		Новый Структура("ПараметрИдУзла, ПараметрИдПроцесса, ПараметрКоманда, ПараметрНадписьНаКнопке",
			стрУзел.Код + Узел.КодУзла,
			Запрос.procid,
			?(Запрос.cmd = "nodeopen" И ПервыйВызов, "nodeclose", "nodeopen"),
			?(Запрос.cmd = "nodeopen" И ПервыйВызов, " - " , " + "))) +
	Шаблон.ПолучитьОбласть("ОбластьКнопкаИмя",
		Новый Структура("ПараметрИдУзла, ПараметрИдПроцесса, ПараметрКоманда, ПараметрНадписьНаКнопке",
			стрУзел.Код + Узел.КодУзла,
			Запрос.procid,
			"nameedit",
			УзелИмяУзла));

	ПараметрЗаголовокУзла = Шаблон.ПолучитьОбласть("ОбластьЗаголовокУзла",
		Новый Структура("ПараметрИмяУзла, ПараметрСвойстваУзла",
			ПараметрИмяУзла,
			Атрибуты));

	Представление = Шаблон.ПолучитьОбласть("ОбластьУзел",
		Новый Структура("ПараметрИдУзла, ПараметрЗаголовокУзла, ПараметрДочернийУзел",
			стрУзел.Код + Узел.КодУзла,
			ПараметрЗаголовокУзла,
			ДочернийУзел));

	Если НЕ ПервыйВызов И НЕ Запрос.cmd = "nodeclose" Тогда
		Если Узел.Свойство("СоседнийУзел") Тогда
				Представление = Представление + ПоказатьСтруктуруУзла(Запрос, Новый Структура("Узел, Код", Узел.СоседнийУзел, стрУзел.Код));
		КонецЕсли;
	КонецЕсли;

	Возврат Представление;

КонецФункции


Функция ПриСозданииОбъекта(ИмяФайлаДанных, знШаблон);

	pagedata = ЗагрузитьСценарий(ИмяФайлаДанных);
	КодУзла = pagedata.КодУзла;
	Данные = pagedata.Данные;
	Шаблон = знШаблон;

КонецФункции
