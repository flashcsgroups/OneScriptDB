// изменяет код скрипта для оценки производительности в разрезе строк.
// результат пишет в .log файл

Функция СтрокаВКод(ТекстовоеСодержимое)
	Возврат СтрЗаменить(ТекстовоеСодержимое, """", """""");
КонецФункции

Функция ОбработатьФайл(ИмяФайлаСкрипта)

	ЧТ = Новый ЧтениеТекста;
	ЧТ.Открыть(ИмяФайлаСкрипта, КодировкаТекста.UTF8);

	ЗТ = Новый ЗаписьТекста(ИмяФайлаСкрипта + "m", КодировкаТекста.UTF8);

	оСтр = ЧТ.ПрочитатьСтроку();
	
	функц = "
	|
	|Перем results;
	|Перем meas_start;
	|
	|Функция Измер(НомерСтроки)
	|	Если meas_start = Неопределено Тогда
	|		meas_start = ТекущаяДата();
	|		results = Новый ТаблицаЗначений;
	|		results.Колонки.Добавить(""НомерСтроки"");
	|		results.Колонки.Добавить(""ТекущаяДата"");
	|	КонецЕсли;
	|	новстр = results.Добавить();
	|	новстр.НомерСтроки = НомерСтроки;
	|	новстр.ТекущаяДата = ТекущаяДата();
	|КонецФункции
	|
	|Функция ВыгрузитьРезультаты(Строки)
	|	meas_file = Новый ЗаписьТекста(""" + ИмяФайлаСкрипта + ".log"");
	|	измпред = meas_start;
	|	Для каждого измкон из results Цикл
	|		стр = Строки.Получить(измкон.НомерСтроки);
	|		meas_file.ЗаписатьСтроку("""" + (измкон.ТекущаяДата - meas_start) + Символы.Таб + (измкон.ТекущаяДата - измпред) + Символы.Таб + измкон.НомерСтроки + Символы.Таб + стр);	
	|		измпред = измкон.ТекущаяДата;
	|	КонецЦикла;
	|	meas_file.Закрыть();
	|КонецФункции
	|
	|";

	ПокаПерем = Истина;
	Стр = "";
	Строки = "";
	
	НомерСтроки = 1;
	Пока оСтр <> Неопределено Цикл // строки читаются до символа перевода строки
		
		Стр = СокрЛП(оСтр);
		Стр = СтрЗаменить(Стр, Символы.Таб, "");
		
		Если НЕ Стр = "" Тогда

			нРегСтр = нРег(Стр);
			Если ПокаПерем Тогда
				Если НЕ Лев(нРегСтр, 6) = "перем " Тогда
					оСтр = функц + оСтр;
					ПокаПерем = Ложь;
				КонецЕсли;
			КонецЕсли;
			Если Прав(Стр,1) = ";" И НЕ Лев(нРегСтр, 2) = "//" И НЕ Лев(нРегСтр, 6) = "перем " И НЕ Найти("конеццикла; конецесли;", нРегСтр) Тогда
				Строки = Строки + "Строки.Вставить(" + НомерСтроки + ", """ + СтрокаВКод(Стр) + """);" + Символы.ПС;
				оСтр = оСтр + Символы.Таб + "Измер(" + НомерСтроки  + ");";
			КонецЕсли;
			
		КонецЕсли;
		ЗТ.ЗаписатьСтроку(оСтр);
		
		оСтр = ЧТ.ПрочитатьСтроку();
		НомерСтроки = НомерСтроки + 1;
	КонецЦикла;

	ЗТ.ЗаписатьСтроку("Строки = Новый Соответствие();
	|" + Строки);

	ЗТ.ЗаписатьСтроку("ВыгрузитьРезультаты(Строки);");
	
	ЧТ.Закрыть();
	ЗТ.Закрыть();

КонецФункции

ИмяФайлаСкрипта = "c:\os\srv\oneshell.os";

ОбработатьФайл(ИмяФайлаСкрипта);

ЗагрузитьСценарий(ИмяФайлаСкрипта + "m");
