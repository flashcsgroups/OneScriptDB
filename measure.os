// изменяет код скрипта для оценки производительности в разрезе строк.
// результат пишет в .log файл

Функция СтрокаВКод(ТекстовоеСодержимое)
	Возврат СтрЗаменить(ТекстовоеСодержимое, """", """""");
КонецФункции

Функция Вставка(ИмяФайлаСкрипта)
	
	Возврат "
	|
	|Var results;
	|Var meass;
	|Var measv;
	|
	|Функция meas(НомерСтроки, measv1)
	|	Если results = Неопределено Тогда
	|		results = Новый ТаблицаЗначений;
	|		results.Колонки.Добавить(""НомерСтроки"");
	|		results.Колонки.Добавить(""ТекущаяДата"");
	|		results.Колонки.Добавить(""Замер"");
	|		meass = ТекущаяДата();
	|	КонецЕсли;
	|	новстр = results.Добавить();
	|	новстр.НомерСтроки = НомерСтроки;
	|	measv = ТекущаяДата();
	|	новстр.ТекущаяДата = measv;
	|	новстр.Замер = новстр.ТекущаяДата - ?(measv1 = Неопределено, measv, measv1);
	|	Возврат measv;
	|КонецФункции
	|
	|Функция meas_end(Строки)
	|	meas_file = Новый ЗаписьТекста(""" + ИмяФайлаСкрипта + ".log"");
	|	meas_all = 0;
	|	Для каждого измкон из results Цикл
	|		стр = Строки.Получить(измкон.НомерСтроки);
	|		meas_file.ЗаписатьСтроку("""" + (измкон.ТекущаяДата - meass) + Символы.Таб + измкон.Замер + Символы.Таб + измкон.НомерСтроки + Символы.Таб + стр);	
	|	КонецЦикла;
	|	meas_file.Закрыть();
	|КонецФункции
	|
	|";

КонецФункции


Функция ОбработатьФайл(ИмяФайлаСкрипта)

	ЧТ = Новый ЧтениеТекста;
	ЧТ.Открыть(ИмяФайлаСкрипта, КодировкаТекста.UTF8);

	ЗТ = Новый ЗаписьТекста(ИмяФайлаСкрипта + "m", КодировкаТекста.UTF8);

	оСтр = ЧТ.ПрочитатьСтроку();

	ПокаПерем = Истина;
	Стр = "";
	Строки = "";
	
	НомерСтроки = 1;
	Пока оСтр <> Неопределено Цикл // строки читаются до символа перевода строки
		
		Стр = СокрЛП(оСтр);
		Стр = СтрЗаменить(Стр, Символы.Таб, "");
		
		Если НЕ Стр = "" Тогда

			нРегСтр = нРег(Стр);
			Если ПокаПерем Тогда
				Если НЕ Лев(нРегСтр, 6) = "перем " И НЕ Лев(нРегСтр, 4) = "var " Тогда
					оСтр = Вставка(ИмяФайлаСкрипта) + оСтр;
					ПокаПерем = Ложь;
				КонецЕсли;
			КонецЕсли;
			Если Прав(Стр,1) = ";" И НЕ Лев(нРегСтр, 2) = "//" И НЕ Лев(нРегСтр, 6) = "перем " И НЕ Лев(нРегСтр, 4) = "var " И НЕ Найти("конеццикла; конецесли; endif; enddo;", нРегСтр) Тогда
				Строки = Строки + "Строки.Вставить(" + НомерСтроки + ", """ + СтрокаВКод(Стр) + """);" + Символы.ПС;
				оСтр = оСтр + Символы.Таб + "measv = meas(" + НомерСтроки  + ", measv);";
			КонецЕсли;
			Если Лев(нРегСтр, 10) = "процедура " ИЛИ Лев(нРегСтр, 8) = "функция " ИЛИ Лев(нРегСтр, 10) = "procedure " ИЛИ Лев(нРегСтр, 9) = "function " Тогда
				оСтр = оСтр + Символы.ПС + "var measv;";
			КонецЕсли;			
		КонецЕсли;
		ЗТ.ЗаписатьСтроку(оСтр);
		
		оСтр = ЧТ.ПрочитатьСтроку();
		НомерСтроки = НомерСтроки + 1;
	КонецЦикла;

	ЗТ.ЗаписатьСтроку("Строки = Новый Соответствие();
	|" + Строки);

	ЗТ.ЗаписатьСтроку("meas_end(Строки);");
	
	ЧТ.Закрыть();
	ЗТ.Закрыть();

КонецФункции

ИмяФайлаСкрипта = "c:\os\srv\oneshell.os";

ОбработатьФайл(ИмяФайлаСкрипта);

ЗагрузитьСценарий(ИмяФайлаСкрипта + "m");
