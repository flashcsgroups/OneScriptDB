// MIT License
// Copyright (c) 2018 Vladimir Vasiliev
// https://github.com/vasvl123/OneScriptDB


Функция СтрокуВСтруктуру(Знач Стр)
	Стр = СтрРазделить(Стр, Символы.Таб);
	Ключ = Неопределено;
	Рез = Неопределено;
	Для Каждого знСтр Из Стр Цикл
		Если Ключ = Неопределено Тогда
			Ключ = знСтр;
		Иначе
			Если Рез = Неопределено Тогда
				Рез = Новый Структура;
			КонецЕсли;
			Рез.Вставить(Ключ, знСтр);
			Ключ = Неопределено;
		КонецЕсли;
	КонецЦикла;
	Возврат Рез;
КонецФункции


Функция СписокВТаблицу(Данные, Параметры) Экспорт
	ЭтотУзел = Параметры.ЭтотУзел;
	ПарШаблон = Неопределено;
	Список = Параметры.Список;
	нУзел = Неопределено;
	РодительУзел = Неопределено;
	Пока НЕ Список = Неопределено Цикл
		нстрУзел = Новый Структура("Имя", "tr");
		Если нУзел = Неопределено Тогда
			нУзел = Данные.НовыйУзел(нстрУзел, Истина);
			нУзел.Вставить("Старший", ЭтотУзел);
			нУзел.Вставить("Родитель", ЭтотУзел);
			РодительУзел = нУзел;
		Иначе
			нУзел = Данные.НовыйСоседний(нУзел, нстрУзел, Истина);
		КонецЕсли;
		// это ссылка на строку таблицы
		Узел = Данные.НовыйДочерний(нУзел, Новый Структура("Имя, Значение", "Узел", "Строка"), Истина);
		Данные.НовыйДочерний(Узел, Новый Структура("Имя, Значение", "Указатель", Список.Код), Истина);
		Атрибут = Данные.Атрибут(Список);
		Пока НЕ Атрибут = Неопределено Цикл
			нстУзел = Новый Структура("Имя, Значение", "td", Атрибут.Значение);
			Если Узел.Имя = "tr" Тогда
				Узел = Данные.НовыйДочерний(нУзел, нстУзел, Истина);
			Иначе
				Узел = Данные.НовыйСоседний(Узел, нстУзел, Истина);
			КонецЕсли;
			// Если есть шаблон для значения
			Если Параметры.Свойство(Атрибут.Имя, ПарШаблон) Тогда
				Узел.Значение = Неопределено;
				Данные.НовыйДочерний(Узел, Новый Структура("Имя, Значение", "К", ПарШаблон.Значение), Истина);
			КонецЕсли;
			Атрибут = Данные.Соседний(Атрибут);
		КонецЦикла;
		Список = Данные.Соседний(Список);
	КонецЦикла;
	Возврат РодительУзел;
КонецФункции


Функция ПокаЦикл(Данные, Параметры) Экспорт
	ЭтотУзел = Параметры.ЭтотУзел;
	УсловиеПока = Параметры.УсловиеПока;
	ЦиклУзел = Параметры.ЦиклУзел;
	Узел = Неопределено;
	Пока Данные.Интерпретировать(УсловиеПока) Цикл
		НовыйУзел = Данные.КопироватьВетку(ЦиклУзел, Узел, ЭтотУзел);
		Если Узел = ЭтотУзел Тогда
			Узел.Вставить("Дочерний", НовыйУзел);
		Иначе
			Узел.Вставить("Соседний", НовыйУзел);
		конецЕсли;
		Узел = НовыйУзел;
	КонецЦикла;
	Возврат Узел;
КонецФункции // ПокаЦикл()


Функция СписокБаз(Данные, Параметры) Экспорт
	Узел = Данные.НовыйУзел(Новый Структура("Имя", "Ссылка"), Истина);
	родУзел = Узел;
	СписокФайлов = НайтиФайлы(ОбъединитьПути(ТекущийКаталог(), "data"), "*.osdb", Истина);
	Если СписокФайлов.Количество() Тогда
		Для каждого элФайл Из СписокФайлов Цикл
			стрУзел = Новый Структура("Имя", "Строка");
			Если Узел.Имя = "Ссылка" Тогда
				Узел = Данные.НовыйДочерний(Узел, стрУзел, Истина);
			Иначе
				Узел = Данные.НовыйСоседний(Узел, стрУзел, Истина);
			КонецЕсли;
			Атр = Данные.НовыйАтрибут(Узел, Новый Структура("Имя, Значение", "ИмяФайла", элФайл.Имя), Истина);
			Атр = Данные.НовыйСоседний(Атр, Новый Структура("Имя, Значение", "ВремяИзменения", элФайл.ПолучитьВремяИзменения()), Истина);
			Атр = Данные.НовыйСоседний(Атр, Новый Структура("Имя, Значение", "Размер", элФайл.Размер()), Истина);
		КонецЦикла;
	КонецЕсли;
	Возврат родУзел;
КонецФункции


Функция СписокДанных(Данные, Параметры) Экспорт
	БазаДанных = Параметры.БазаДанных;
	ИндексИмя = ОбъединитьПути(ТекущийКаталог(), "data", БазаДанных + ".files", "index");
	Индекс = Новый Файл(ИндексИмя);
	Если НЕ Индекс.Существует() Тогда
		Соединение = Неопределено;
		Данные.ПередатьСтроку(Соединение, "osdb	" + БазаДанных);
		Попытка
			Соединение.ПрочитатьСтроку();
			Соединение.Закрыть();
		Исключение
			Сообщить(ОписаниеОшибки());
			Возврат Неопределено;
		КонецПопытки;
	КонецЕсли;
	Индекс = Новый ТекстовыйДокумент;
	Индекс.Прочитать(ИндексИмя);
	Узел = Данные.НовыйУзел(Новый Структура("Имя", "Ссылка"), Истина);
	родУзел = Узел;
	Для н = 1 По Индекс.КоличествоСтрок() Цикл
		нУзел = СтрокуВСтруктуру(Индекс.ПолучитьСтроку(н));
		стрУзел = Новый Структура("Имя", "Строка");
		Если Узел.Имя = "Ссылка" Тогда
			Узел = Данные.НовыйДочерний(Узел, стрУзел, Истина);
		Иначе
			Узел = Данные.НовыйСоседний(Узел, стрУзел, Истина);
		КонецЕсли;
		Атр = Данные.НовыйАтрибут(Узел, Новый Структура("Имя, Значение", "ИмяФайла", нУзел.data), Истина);
		Атр = Данные.НовыйСоседний(Атр, Новый Структура("Имя, Значение", "ПозицияДанных", нУзел.dataposition), Истина);
		Атр = Данные.НовыйСоседний(Атр, Новый Структура("Имя, Значение", "ВремяИзменения", "" + Данные.УзелСвойство(нУзел, "date")), Истина);
		Атр = Данные.НовыйСоседний(Атр, Новый Структура("Имя, Значение", "Размер", нУзел.length), Истина);
	КонецЦикла;
	Возврат родУзел;
КонецФункции


Функция СписокФайлов(Данные, Параметры) Экспорт
	Узел = Данные.НовыйУзел(Новый Структура("Имя", "Ссылка"), Истина);
	родУзел = Узел;
	СписокФайлов = НайтиФайлы(ОбъединитьПути(ТекущийКаталог(), "data", ".files"), "*.*", Истина);
	Если СписокФайлов.Количество() Тогда
		Для каждого элФайл Из СписокФайлов Цикл
			стрУзел = Новый Структура("Имя", "Строка");
			Если Узел.Имя = "Ссылка" Тогда
				Узел = Данные.НовыйДочерний(Узел, стрУзел, Истина);
			Иначе
				Узел = Данные.НовыйСоседний(Узел, стрУзел, Истина);
			КонецЕсли;
			Атр = Данные.НовыйАтрибут(Узел, Новый Структура("Имя, Значение", "ИмяФайла", элФайл.Имя), Истина);
			Атр = Данные.НовыйСоседний(Атр, Новый Структура("Имя, Значение", "ВремяИзменения", элФайл.ПолучитьВремяИзменения()), Истина);
			Атр = Данные.НовыйСоседний(Атр, Новый Структура("Имя, Значение", "Размер", элФайл.Размер()), Истина);
		КонецЦикла;
	КонецЕсли;
	Возврат родУзел;
КонецФункции
